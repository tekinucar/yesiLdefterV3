/*CreateTable*/

begin transaction

 create table MtskAdaySozlesme
 (
   Id                    Int IDENTITY(1,1) not null, 
   FirmId                Int       not null,
   TalepId               Int       not null, 
   AdayId                Int       not null, 
   TcNo                  Varchar(11)   null,
   DonemTipiId           Int           null,
   VeriKaynakId          Int           null, /* Veri transferi edildiği tablodaki Id . Sorun olduğunda bilginin nereden geldiğine referans olsun */

   SozlesmeTarihi        Date          null,
   Sozlesme1Resim        VarBinary(max) null,
   Sozlesme2Resim        VarBinary(max) null,
   Sozlesme1ResimSmall   VarBinary(max) null,
   Sozlesme2ResimSmall   VarBinary(max) null,

   constraint		pk_MtskAdaySozlesme primary key (Id)
 )
  
 grant select, insert, update, delete on MtskAdaySozlesme to public

  create index X_MtskAdaySozlesme01 on MtskAdaySozlesme (TalepId)
  create index X_MtskAdaySozlesme02 on MtskAdaySozlesme (AdayId)
  create index X_MtskAdaySozlesme03 on MtskAdaySozlesme (TcNo)
  create index X_MtskAdaySozlesme04 on MtskAdaySozlesme (DonemTipiId)

commit transaction

/*CreateTableEnd*/

/*CreateTrigger*/

CREATE TRIGGER dbo.trg_MtskAdaySozlesme on dbo.MtskAdaySozlesme
AFTER INSERT, UPDATE
AS BEGIN

    DECLARE sozlesmeCursor cursor local for select Id from Inserted
    DECLARE @sozlesmeId bigint

    OPEN sozlesmeCursor
    FETCH NEXT FROM sozlesmeCursor INTO @sozlesmeId

    -- Veri Transferi başladımı
    declare @DbUpdatesIsActive Bit = 0
    set @DbUpdatesIsActive = [dbo].[fnc_getIsDbUpdate] (13)

    Declare @TalepId int = 0
	declare @AdayId int
	declare @VeriKaynakId int

    WHILE @@FETCH_STATUS = 0
    BEGIN

        select 
            @TalepId = ins.TalepId
           ,@AdayId = ins.AdayId
		   ,@VeriKaynakId = ins.VeriKaynakId
		from inserted ins
    	where ins.Id = @sozlesmeId

        if (@VeriKaynakId > 0 and (@TalepId = 0 or @AdayId = 0))
        begin
            Update dbo.MtskAdaySozlesme set
                TalepId = t.Id  
               ,AdayId  = t.AdayId 
            From dbo.MtskAdaySozlesme as a,
                 dbo.MtskAdayTalep as t
            Where a.VeriKaynakId = t.VeriKaynakId				 
            and   a.Id = @sozlesmeId

			Select @TalepId = Id, @AdayId = AdayId 
			From dbo.MtskAdayTalep 
			Where VeriKaynakId = @VeriKaynakId
        end

		-- MtskAdayTalep tablosunu işaretle
        if (@DbUpdatesIsActive = 0)
        begin
            Update MtskAdayTalep set
                SozlesmeYapildi = x.Sonuc
            From MtskAdayTalep a, 
            (
               Select count(Id) as Sonuc
               From MtskAdaySozlesme
               Where Sozlesme1Resim is not null
               and TalepId = @TalepId
            ) as x
            Where a.Id = @TalepId
        end

		-- MtskAdayBelgeler tablosunu işaretle
		Update MtskAdayBelgeler set
        Sozlesme1Tarandi = x.Sonuc
        From MtskAdayBelgeler a, 
        (
            Select count(Id) as Sonuc
            From MtskAdaySozlesme
            Where 0 = 0
            and Sozlesme1Resim is not null
            and TalepId = @TalepId
        ) as x
        Where a.TalepId = @TalepId

        Update MtskAdayBelgeler set
        Sozlesme2Tarandi = x.Sonuc
        From MtskAdayBelgeler a, 
        (
            Select count(Id) as Sonuc
            From MtskAdaySozlesme
            Where 0 = 0
            and Sozlesme2Resim is not null
            and TalepId = @TalepId
        ) as x
        Where a.TalepId = @TalepId

        -- Data transferi oldu fakat daha önce bu talep için boş kayıt oluşturulmuş ise onu sil
        if (@VeriKaynakId > 0)
        begin
		    Delete From dbo.MtskAdaySozlesme
            Where TalepId = @TalepId
            and   isnull(VeriKaynakId,0) = 0
        end

        FETCH NEXT FROM sozlesmeCursor INTO @sozlesmeId
    END -- While

    CLOSE sozlesmeCursor
    DEALLOCATE sozlesmeCursor   


END -- create trigger

/*CreateTriggerEnd*/

ALTER TABLE dbo.MtskAdaySozlesme ENABLE TRIGGER trg_MtskAdaySozlesme


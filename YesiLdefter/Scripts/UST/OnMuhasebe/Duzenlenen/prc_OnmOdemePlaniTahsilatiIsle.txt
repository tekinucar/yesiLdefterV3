/*CreateProcedure*/

CREATE PROCEDURE [dbo].[prc_OnmOdemePlaniTahsilatiIsle] (@dekontId int)  
AS BEGIN 

--declare @dekontId int
--set @dekontId = 15

  /* gelen dekontId daha öşlenmiş mi kontrl et, eğer işlenmişse geri dön  */
  declare @adet int = 0
  Select @adet = count(Id) from dbo.OnmOdemePlani 
  Where BelgeDekontId = @dekontId 
  if (@adet > 0) return

  declare @BelgeTarihi datetime
  declare @AHesapTipiId int 
  declare @BelgeTutari numeric(22,5)
  declare @SourceTypeId smallInt
  declare @SourceId Int
  declare @VeriKaynakId Int
  declare @talepId int -- = SourceId

  declare @odemePlaniId int
  declare @TaksitTutari numeric(22,5)
  declare @KalanTutar numeric(22,5)

  /*
  OnmBelgeDekontSourceTypeId
  ( 0,   N'Tanımsız'),
  ( 1,   N'OnmBelgeMaliyet'),
  ( 2,   N'OnmBelgeDekont - Kendisi'), -- kaynak kendisi
  ( 3,   N'OnmBankaKredisiS'),
  ( 201, N'UstadMtsk.OnmOdemePlani'),
  ( 202, N'UstadIsmak.OnmOdemePlani'),
  ( 203, N'UstadSrc.OnmOdemePlani'),
  */

  set @BelgeTutari = 0
  select 
       @BelgeTarihi = ins.BelgeTarihi
     , @AHesapTipiId = isnull(ins.AHesapTipiId,0) 
     , @BelgeTutari = round(isnull(ins.BelgeTutari,0),2)
     , @talepId = ins.TalepId 
     , @SourceTypeId = ins.SourceTypeId
     , @SourceId = ins.SourceId -- mevcut düzende TalepId barındıryor
     , @VeriKaynakId = isnull(ins.VeriKaynakId,0)
   from dbo.OnmBelgeDekont as ins
   where ins.IsActive = 1
   and   isnull(ins.IsIptal,0) = 0 
   and   ins.Id = @dekontId 

   -- Tahsil edilen tutarı kalanTutar sayesinde parçalayacağız
   -- ilk atama
   set @KalanTutar = @BelgeTutari 

   -- Döngü içinde kalanTutar bitene kadar işlemler devam edecek
   while (@KalanTutar > 0)
   begin
       -- normal kayıtlar çalışacak : @SourceTypeId : 201,202,203 ?.OnmOdemePlani 
       if (@VeriKaynakId = 0) 
       begin
           -- Tahsil edilmemiş ilk sıradaki taksit
           Select top 1 @odemePlaniId = Id, @TaksitTutari = TaksitTutari 
           from dbo.OnmOdemePlani 
           Where IsActive = 1
           and IsPaid = 0
           and TalepId = @TalepId
           and UcretTipiId = @AHesapTipiId -- Hangi ücret türü
           order by VadeTarihi, Id
       end
       -- veri transfer kayıtları çalışacak
       if (@VeriKaynakId > 0) 
       begin
           -- Tahsil edilmemiş ilk sıradaki taksit
           Select top 1 @odemePlaniId = Id, @TaksitTutari = TaksitTutari 
           from dbo.OnmOdemePlani 
           Where IsActive = 1
           and IsPaid = 0
           and TalepId = @TalepId
           and VeriKaynakId = @VeriKaynakId -- TransferDatabase.Muhasebe.Id
           order by VadeTarihi, Id
       end

       -- @KalanTutar - eksiye düşüyorsa
       if (@KalanTutar < @TaksitTutari)
       begin
           -- Ödeme planını eksik ödeme yapınca
           Update dbo.OnmOdemePlani set
               YedekTaksitTutari = TaksitTutari
             , TaksitTutari = @KalanTutar
             , TahsilTarihi = @BelgeTarihi
             , IsPaid = 1
             , BelgeDekontId = @dekontId 
           Where Id = @odemePlaniId

           -- Kalan TaksitTutarı için yeni satır oluştur
           Insert into dbo.OnmOdemePlani
               ( FirmId, IsActive, DonemTipiId, TalepId, TalepTipiId, UcretTipiId, CariId, TaksitTipiId
                ,TaksitNo, VadeTarihi 
                ,TaksitTutari, IsPaid
                ,YedekTaksitTutari, BelgeDekontId, PatchBelgeDekontId, BelgeStokBId )
                Select 
                 FirmId,        1, DonemTipiId, TalepId, TalepTipiId, UcretTipiId, CariId, TaksitTipiId
                ,TaksitNo, VadeTarihi 
                ,@TaksitTutari - @KalanTutar  -- eksik olan tutar
                ,0      --IsPaid
                ,null   --YedekTaksitTutari
                ,null   --BelgeDekontId
                ,@dekontId  --PatchBelgeDekontId ? bu Id sayesinde hangi tahsilat makbuzu yüzünden (eksik tahsilat) oluştuğu bulunuyor, yani sebep olan OnmBelgeDekont.Id
                ,null   --BelgeStokBId
               from dbo.OnmOdemePlani
	           where Id = @odemePlaniId
       end -- @KalanTutar < @TaksitTutari

       -- @KalanTutar eşit veya fazla geliyorsa
       if (@KalanTutar >= @TaksitTutari)
       begin
           --print 'fazla para'
           Update dbo.OnmOdemePlani set
               IsPaid = 1
             , TahsilTarihi = @BelgeTarihi
             , BelgeDekontId = @dekontId 
           Where Id = @odemePlaniId
       end

       set @KalanTutar = @KalanTutar - @TaksitTutari

   end -- While

END -- prc_

/*CreateProcedureEnd*/
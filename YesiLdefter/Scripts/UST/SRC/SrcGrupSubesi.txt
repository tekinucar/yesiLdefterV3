/*CreateTable*/

begin transaction

 create table SrcGrupSubesi
 (
   Id                      Int IDENTITY(1,1) not null, 
   ParentId                Int       not null, /* default 0 */
   FirmId                  Int       not null,
   IsActive                Bit       not null, /* default : 1 = active, 0 = not active */  

   DonemTipiId             Int           null,
   GrupTipiId              SmallInt      null,
   SubeTipiId              SmallInt      null,
   GrupVeSube              varchar(20)   null, 
   SertifikaTipiId         SmallInt      null, /* detayı [Lkp].[SrcSertifikaTipi] tablosunda */
   SertifikaSinifTipiId    SmallInt      null, /* İstenen SRC5'in detay sertifika tipi Id si */

   DonemGrupSubeSertifika  varchar(50)   null, /*2025 Ağustos, 1 Grup, A Şubesi : SRC 1-2-3-4  */
   MebbisDonemKodu         Varchar(20)   null, /* MEBBİS den gelen veya gönderilen dönem kodu */

   GrupBaslamaTarihi       Date          null,
   GrupBitisTarihi         Date          null,
   SubeAdaySayisi          SmallInt      null,
   KurumOnayTipiId         SmallInt      null,  /* 0. Onay bekliyor, 1-Onaylandı  tablo = [Lkp].[SrcKurumOnayTipi]   */		
   KapasiteTipiId          SmallInt      null,  /* şubenin doluluk durumu hakkında */ 

   TakipTipiId             SmallInt      null,
   DerslikAdiTipiId        SmallInt      null,
   ToplamDersSayisi        SmallInt      null,   
   TDersIsUploaded         Bit           null, /* Teorik derslerin tamamı Mebbise yüklenince 1 oluyor */
      
   constraint		pk_SrcGrupSubesi primary key (Id)
 )
  
 grant select, insert, update, delete on SrcGrupSubesi to public

commit transaction

/*CreateTableEnd*/

/*  MebbisDonemKodu içeriği

Mebbis sayfasındaki kodlama için örnekler 

<option selected="selected" value="201707-1-1-10001">2017 - Temmuz-SRC (1,2,3,4) Karma Mesleki Yeterlilik Kurs Programı-Grup 1-A ŞUBESİ</option>
<option value="201707-2-2-10001">2017 - Temmuz-SRC (1,2,3,4) Karma Mesleki Yeterlilik Kurs Programı-Grup 2-B ŞUBESİ</option>

"201707-1-1-10001"  YilAy-GrupTipiId-SubeTipiId-SertifikaTipiId
"201707-2-2-10001"  

*/



/*CreateTrigger*/

CREATE TRIGGER [dbo].[trg_SrcGrupSubesi] on [dbo].[SrcGrupSubesi] 
AFTER INSERT, UPDATE
AS BEGIN

    DECLARE mgSubesiCursor cursor local for select Id from Inserted
    DECLARE @curId bigint

    OPEN mgSubesiCursor
    FETCH NEXT FROM mgSubesiCursor INTO @curId
   
    declare @SrcDonemiId int 
    declare @FirmId int
    declare @DonemTipiId int
    declare @GrupTipiId smallint
    declare @SubeTipiId smallint
    declare @SertifikaTipiId smallInt
    declare @GrupBaslamaTarihi date
    declare @KurumOnayTipiId smallint
    declare @LkpSrcDonemiId int

	declare @DonemGrupBaslamaTarihi date
    declare @eDonemTipiId int
    declare @eGrupTipiId smallint
    declare @eSubeTipiId smallint
    declare @eSertifikaTipiId smallInt
    declare @DonemTipiAdi varchar(15)
    declare @GrupAdi varchar(10)
    declare @SubeAdi varchar(10)
    declare @GrupVeSube varchar(20)
    declare @SertifikaAdi varchar(15)
    declare @SertifikaMebbisKodu varchar(10)
    declare @DonemGrupSubeSertifika varchar(50)
    declare @MebbisDonemKodu varchar(20)

    WHILE @@FETCH_STATUS = 0
    BEGIN

      Select
         @eDonemTipiId  = isnull(DonemTipiId,0)
        ,@eGrupTipiId  = isnull(GrupTipiId,0)
        ,@eSubeTipiId  = isnull(SubeTipiId,0)
        ,@eSertifikaTipiId  = isnull(SertifikaTipiId,0)
      From deleted dlt
	  Where dlt.Id = @curId

      Select
         @FirmId = FirmId
        ,@DonemTipiId = isnull(DonemTipiId,0)
        ,@GrupTipiId  = isnull(GrupTipiId,0)
        ,@SubeTipiId  = isnull(SubeTipiId,0)
        ,@SertifikaTipiId = isnull(SertifikaTipiId,0) 
        ,@GrupBaslamaTarihi = GrupBaslamaTarihi
        ,@KurumOnayTipiId = KurumOnayTipiId

        ,@GrupVeSube = isnull(GrupVeSube,'')
        ,@DonemGrupSubeSertifika = isnull(DonemGrupSubeSertifika,'')
        ,@MebbisDonemKodu = isnull(MebbisDonemKodu,'')

      From inserted ins
	  Where ins.Id = @curId

      set @SrcDonemiId = 0
      set @LkpSrcDonemiId = 0

      -- SrcDonemi tablosunda gerekli olan satır mevcut mu ?
      Select @SrcDonemiId = isnull(Id, 0)
	       , @DonemGrupBaslamaTarihi = GrupBaslamaTarihi
      From dbo.SrcDonemi
      Where FirmId = @FirmId
      and DonemTipiId = @DonemTipiId
      and GrupTipiId = @GrupTipiId

      -- Sube belli olduktan sonra SubeTipiId = 0 olanlanlar silinsin
      if (@DonemTipiId > 0) and  
         (@GrupTipiId > 0) and 
         (@SubeTipiId > 0)
      begin
         Delete From dbo.SrcGrupSubesi
         Where FirmId = @FirmId
         and DonemTipiId = @DonemTipiId
         and GrupTipiId = @GrupTipiId
         and SubeTipiId <= 0
      end
      
	  if ((@GrupBaslamaTarihi is null) and 
	      (@DonemGrupBaslamaTarihi is not null))
      begin
          Update dbo.SrcGrupSubesi Set
              GrupBaslamaTarihi = @DonemGrupBaslamaTarihi
          Where Id = @curId
      end

      if (@SrcDonemiId = 0)
      begin
         INSERT INTO [dbo].[SrcDonemi]
           ([ParentId]
           ,[FirmId]
           ,[IsActive]
           ,[DonemTipiId]
           ,[GrupTipiId]
           ,[GrupBaslamaTarihi]
           ,[GrupSubeSayisi]
           ,[DonemAdaySayisi]
           ,[TeorikDersSayisi]
           ,[UygulamaDersSayisi]
           ,[KurumOnayTipiId]
           ,[GrupAdaySayisi])
         VALUES
           (0
           ,@FirmId
           ,1
           ,@DonemTipiId
           ,@GrupTipiId
           ,@GrupBaslamaTarihi
           ,0 --GrupSubeSayisi
           ,0 --DonemAdaySayisi
           ,0 --TeorikDersSayisi
           ,0 --UygulamaDersSayisi
           ,@KurumOnayTipiId
           ,0 --GrupAdaySayisi
           )
      end


      if ( @DonemTipiId <> @eDonemTipiId or
           @GrupTipiId <> @eGrupTipiId or 
           @SubeTipiId <> @eSubeTipiId or 
           @SertifikaTipiId <> @eSertifikaTipiId or
           @GrupVeSube = '' or
           @DonemGrupSubeSertifika = '' or
           @MebbisDonemKodu = ''
           )
      begin
          Select @DonemTipiAdi = DonemTipi From Lkp.SrcDonemTipi Where Id = @DonemTipiId
          Select @GrupAdi = GrupTipi From Lkp.SrcGrupTipi Where Id = @GrupTipiId 
          Select @SubeAdi = SubeTipi From Lkp.SrcSubeTipi Where Id = @SubeTipiId 
          Select @SertifikaAdi = SertifikaTipi, @SertifikaMebbisKodu = MebbisKodu From Lkp.SrcSertifikaTipi Where Id = @SertifikaTipiId

          Set @DonemGrupSubeSertifika = @DonemTipiAdi + ' ' + @GrupAdi + ' ' + @SubeAdi + ' ' + @SertifikaAdi
          Set @MebbisDonemKodu = convert(varchar(6),@DonemTipiId) + '-' + convert(varchar(2),@GrupTipiId) + '-' + convert(varchar(2),@SubeTipiId) + '-' + @SertifikaMebbisKodu

          Update dbo.SrcGrupSubesi Set
              GrupVeSube = @GrupAdi + ' ' + @SubeAdi
            , DonemGrupSubeSertifika = @DonemGrupSubeSertifika
            , MebbisDonemKodu = @MebbisDonemKodu
          Where Id = @curId
      end
    
      --Execute [dbo].[prc_SrcGrupSubesiTeorikDersToplami] @FirmId, @DonemTipiId, @GrupTipiId, @SubeTipiId
	  --Execute [dbo].[prc_SrcGrupSubesiTakipSet] @FirmId, @DonemTipiId, @GrupTipiId, @SubeTipiId
	  --Execute [dbo].[prc_SrcAdayTakipTeorikDersSet] @FirmId, @DonemTipiId, @GrupTipiId, @SubeTipiId

      FETCH NEXT FROM mgSubesiCursor INTO @curId
    END

    CLOSE mgSubesiCursor
    DEALLOCATE mgSubesiCursor   

END

/*CreateTriggerEnd*/

/*CreateTrigger*/

CREATE TRIGGER [dbo].[trg_SrcGrupSubesiDeleteIsLock] on [dbo].[SrcGrupSubesi]
INSTEAD of DELETE
AS BEGIN

    declare isLockCursorDlt cursor local for select Id from deleted
    declare @curId bigint

    OPEN isLockCursorDlt

    FETCH NEXT FROM isLockCursorDlt INTO @curId
	   
    WHILE @@FETCH_STATUS = 0
    BEGIN
        if ( Select COUNT(t.Id) as Adet from deleted as d
             left join dbo.SrcAdayTalep as t on ( d.DonemTipiId = t.DonemTipiId 
                and d.GrupTipiId = t.GrupTipiId  
                and d.SubeTipiId = t.SubeTipiId )
        ) > 0 
        begin
            PRINT 'Bu kayıt silinemez!'
            RETURN
        end
	    IF EXISTS (SELECT 1 FROM deleted WHERE isnull(GrupTipiId,0) = 1 and isnull(SubeTipiId,0) = 1 and Id = @curId)
        BEGIN
            PRINT 'Bu kayıt silinemez!'
            RETURN
        END

        Delete from dbo.SrcGrupSubesi Where Id = @curId

      FETCH NEXT FROM isLockCursorDlt INTO @curId
    END

    CLOSE isLockCursorDlt
    DEALLOCATE isLockCursorDlt

END

 /*CreateTriggerEnd*/


/*CreateLkpTable*/

begin transaction

-- ------------------------------------------------------- 
-- TakipTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'SrcGrupSubesiTakipTipi')
drop table [Lkp].[SrcGrupSubesiTakipTipi]

create table Lkp.SrcGrupSubesiTakipTipi
(
   Id          Int Unique Not Null,
   TakipTipi   nVarchar(50)   null
   
   constraint  pk_SrcGrupSubesiTakipTipi primary key (Id)
)

DELETE Lkp.SrcGrupSubesiTakipTipi
INSERT INTO Lkp.SrcGrupSubesiTakipTipi (Id, TakipTipi)
 VALUES 
  ( 0,  N'Tanımsız'),
  ( 1,  N'Teorik ders planı yok'),
  ( 2,  N'Teorik ders planı eksik'),
  ( 3,  N'Teorik ders planı mevcut'),

  ( 11, N'Grup başlangıç tarihi belli değil'),
  ( 12, N'Teorik dersler planlanacak'),
  ( 13, N'Teorik dersler planlandı, Mebbise aktarılacak'),
  ( 14, N'Teorik dersler Mebbise aktarıldı'),
  ( 15, N'Teorik dersler başlamadı'),
  ( 16, N'Teorik dersler başladı'),
  ( 17, N'Teorik dersler tamamlandı')

-- ------------------------------------------------------- 
-- KapasiteTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'SrcGrupSubesiKapasiteTipi')
drop table [Lkp].[SrcGrupSubesiKapasiteTipi]

create table Lkp.SrcGrupSubesiKapasiteTipi
(
   Id                 Int Unique Not Null,
   KapasiteTipi       nVarchar(50)   null
   
   constraint  pk_SrcGrupSubesiKapasiteTipi primary key (Id)
)

DELETE Lkp.SrcGrupSubesiKapasiteTipi
INSERT INTO Lkp.SrcGrupSubesiKapasiteTipi (Id, KapasiteTipi)
 VALUES 
  ( -6,  N''),
  ( -5,  N'Son 5 aday'),
  ( -4,  N'Son 4 aday'),
  ( -3,  N'Son 3 aday'),
  ( -2,  N'Son 2 aday'),
  ( -1,  N'Son 1 aday'),
  (  0,  N'Kapasite tam dolu'),
  (  1,  N'Sorun : Kapasite fazlası var')


-- ------------------------------------------------------- 
-- DerslikAdiTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'SrcGrupSubesiDerslikAdiTipi')
drop table [Lkp].[SrcGrupSubesiDerslikAdiTipi]

create table Lkp.SrcGrupSubesiDerslikAdiTipi
(
   Id             Int Unique Not Null,
   IsActive       Bit            null, 
   DerslikAdiTipi nVarchar(10)   null,

   constraint  pk_SrcGrupSubesiDerslikAdiTipi primary key (Id)
)

INSERT INTO Lkp.SrcGrupSubesiDerslikAdiTipi (Id, IsActive, DerslikAdiTipi)
 VALUES 
  ( 0, 0, N''),
  ( 1, 0, N'Derslik-1'),
  ( 2, 0, N'Derslik-2'),
  ( 3, 0, N'Derslik-3'),
  ( 4, 0, N'Derslik-4'),
  ( 5, 0, N'Derslik-5'),
  ( 6, 0, N'Derslik-6'),
  ( 7, 0, N'Derslik-7'),
  ( 8, 0, N'Derslik-8'),
  ( 9, 0, N'Derslik-9'),
  (10, 0, N'Derslik-10')



commit transaction

/*CreateLkpTableEnd*/


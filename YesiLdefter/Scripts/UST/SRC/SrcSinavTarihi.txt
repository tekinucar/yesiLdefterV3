/*CreateTable*/

begin transaction

 create table SrcSinavTarihi
 (
   
   Id                       Int IDENTITY(1,1) not null, 
   FirmId                   Int       not null,
   IsActive                 Bit       not null, /* default : 1 = active, 0 = not active */  
   --10
   SinavTipiId              SmallInt      null,
   SinavDonemTipiId         Int           null,
   SinavTarihSaat           Varchar(20)   null,
   SinavTarihi              Date          null,
   SinavSaati               Time          null,
   MebbisESinavTarihValue   Varchar(20)   null, /* mebbis valuesi */ 
   MebbisUSinavTarihValue   Varchar(20)   null, /* mebbis valuesi */

   --20
   AdaySayisi               SmallInt      null,
   BasariliAdaySayisi       SmallInt      null,
   BasarisizAdaySayisi      SmallInt      null,
   --30
   VeriKaynakId             Int           null, /* veri transferlerinde kullanılmak üzere */

   constraint		pk_SrcSinavTarihi primary key (Id)
 )
  
 grant select, insert, update, delete on SrcSinavTarihi to public

 create index X_SrcSinavTarihi01 on SrcSinavTarihi (SinavDonemTipiId desc, SinavTarihi desc)
 
commit transaction

/*CreateTableEnd*/


/*CreateTrigger*/

CREATE TRIGGER [dbo].[trg_SrcSinavTarihi] on [dbo].[SrcSinavTarihi] 
AFTER INSERT,UPDATE
AS 
BEGIN

    declare myCursor cursor local for select Id from Inserted 
    declare @curId bigint

    OPEN myCursor

	declare @ySinavTipiId smallInt
    declare @SinavDonemTipiId int
	declare @ySinavTarihSaat varchar(20)
	declare @eSinavTarihi  date
	declare @ySinavTarihi  date
	declare @ySinavSaati time
	declare @yMebbisESinavTarihValue varchar(20)
    declare @yMebbisUSinavTarihValue varchar(20)

    FETCH NEXT FROM myCursor INTO @curId

    WHILE @@FETCH_STATUS = 0
    BEGIN
        select 
		    @eSinavTarihi = isnull(dlt.SinavTarihi, CONVERT(date, getdate(),103))
        from deleted dlt
        where dlt.Id = @curId

        select 
            @ySinavTipiId = isnull(ins.SinavTipiId,0)
		  , @ySinavTarihSaat = isnull(ins.SinavTarihSaat,'')
		  , @ySinavTarihi = isnull(ins.SinavTarihi, CONVERT(date, getdate(),103))
          , @yMebbisESinavTarihValue = isnull(ins.MebbisESinavTarihValue, '')
          , @yMebbisUSinavTarihValue = isnull(ins.MebbisUSinavTarihValue, '')
        from inserted ins
        where ins.Id = @curId
		
        -- E Sınav
        if (ISDATE(@ySinavTarihSaat) = 1 and @ySinavTipiId = 1)
        begin
            set @ySinavTarihSaat = replace(@ySinavTarihSaat, '/E-Sınav', '') 
            set @ySinavTarihi = convert(date, @ySinavTarihSaat, 103)
            set @ySinavSaati = convert(time, @ySinavTarihSaat, 103)
        end
				
        -- <option value='12020134?05/12/2021'>05/12/2021</option>
        if (@ySinavTipiId < 10 and
            @yMebbisUSinavTarihValue <> '' and
            @yMebbisUSinavTarihValue <> '-1')
        begin
            set @ySinavTarihi = CONVERT(date, substring(@yMebbisUSinavTarihValue, CHARINDEX('?', @yMebbisUSinavTarihValue) + 1, 10), 103)
        end 

        if (MONTH(@ySinavTarihi) < 10)
             set @SinavDonemTipiId = convert(int, convert(varchar(5), YEAR(@ySinavTarihi)) + '0' + convert(varchar(3), MONTH(@ySinavTarihi)))
        else set @SinavDonemTipiId = convert(int, convert(varchar(5), YEAR(@ySinavTarihi)) + convert(varchar(3), MONTH(@ySinavTarihi)))
		
        Update SrcSinavTarihi set
            SinavTarihi = @ySinavTarihi
          , SinavSaati = @ySinavSaati 
          , SinavDonemTipiId = @SinavDonemTipiId 
		Where Id = @curId

        FETCH NEXT FROM myCursor INTO @curId
    END

    CLOSE myCursor
    DEALLOCATE myCursor  

END

/*CreateTriggerEnd*/



/*CreateTrigger*/

CREATE TRIGGER [dbo].[trg_SrcSinavTarihiDelete] on [dbo].[SrcSinavTarihi]
AFTER DELETE
AS BEGIN

    declare cursorDlt cursor local for select Id from deleted
	declare @curId bigint

	OPEN cursorDlt
    FETCH NEXT FROM cursorDlt INTO @curId

    declare @FirmId int 
    declare @SinavTarihi date
	declare @SinavTipiId smallInt

    WHILE @@FETCH_STATUS = 0
    BEGIN
	    Select @FirmId = dlt.FirmId
		, @SinavTarihi = isnull(dlt.SinavTarihi, convert(date, '01.01.1900', 103))
		, @SinavTipiId = dlt.SinavTipiId
	    From deleted dlt
	    Where dlt.Id = @curId
	  
        if (@SinavTipiId = 11)
        begin
            Update dbo.SrcAdayTakip Set 
                eSinavPlanTarihi = null
            Where eSinavPlanTarihi = @SinavTarihi 
            and   FirmId = @FirmId
        end
        if (@SinavTipiId = 12)
        begin
            Update dbo.SrcAdayTakip Set 
                uSinavPlanTarihi = null
            Where uSinavPlanTarihi = @SinavTarihi
            and   FirmId = @FirmId
        end
/*
		Delete From dbo.SrcSinavListesi
        Where  FirmId = @FirmId
        and    SinavTipiId = @SinavTipiId                
        and    SinavTarihi = @SinavTarihi
*/		
        FETCH NEXT FROM cursorDlt INTO @curId
    END

    CLOSE cursorDlt
    DEALLOCATE cursorDlt

END

/*CreateTriggerEnd*/


/*CreateTrigger*/

CREATE TRIGGER [dbo].[trg_SrcSinavTarihiDeleteIsLock] on [dbo].[SrcSinavTarihi]
INSTEAD of DELETE
AS BEGIN

    declare isLockCursorDlt cursor local for select Id from deleted
    declare @curId bigint

    OPEN isLockCursorDlt

    FETCH NEXT FROM isLockCursorDlt INTO @curId
    declare @SinavTarihi date

    WHILE @@FETCH_STATUS = 0
    BEGIN
        Select @SinavTarihi = SinavTarihi 
        From deleted
        Where Id = @curId

        if ( Select COUNT(Id) as Adet From dbo.SrcSinavETeorik
             Where convert(date,SinavTarihi,103) = convert(date,@SinavTarihi,103) ) > 0
        begin
            PRINT 'Bu kayıt silinemez!. Bu tarihte teorik sınav kayıtları mevcut...'
            RETURN
        end

        if ( Select COUNT(Id) as Adet From dbo.SrcSinavUygulama
             Where convert(date,SinavTarihi,103) = convert(date,@SinavTarihi,103) ) > 0
        begin
            PRINT 'Bu kayıt silinemez!. Bu tarihte uygulamalı sınav kayıtları mevcut...'
            RETURN
        end

        Delete from dbo.SrcSinavTarihi Where Id = @curId

      FETCH NEXT FROM isLockCursorDlt INTO @curId
    END

    CLOSE isLockCursorDlt
    DEALLOCATE isLockCursorDlt

END

/*CreateTriggerEnd*/





/*CreateLkpTable*/

begin transaction

-- ------------------------------------------------------- 
-- SinavTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'SrcSinavTarihiSinavTipi')
drop table [Lkp].[SrcSinavTarihiSinavTipi]

create table [Lkp].[SrcSinavTarihiSinavTipi]
(
   Id               Int Unique not null, 
   SinavTipi        nVarchar(20)   null
   
   constraint  pk_SrcSinavTarihiSinavTipi primary key (Id)
)

Delete from [Lkp].[SrcSinavTarihiSinavTipi]
INSERT INTO [Lkp].[SrcSinavTarihiSinavTipi] (Id, SinavTipi)
 VALUES 
  ( 0,    N''),
  ( 1,    N'e-Sınav'),
  ( 2,    N'Uygulama'),
  ( 11,   N'e-Sınav (manuel)'),
  ( 12,   N'Uygulama (manuel)')


commit transaction

/*CreateLkpTableEnd*/

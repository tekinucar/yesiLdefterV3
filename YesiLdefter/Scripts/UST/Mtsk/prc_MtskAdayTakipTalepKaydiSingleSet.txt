/*CreateProcedure*/

CREATE PROCEDURE [dbo].[prc_MtskAdayTakipTalepKaydiSingleSet] (@TalepId int)  
AS BEGIN

    --declare @TalepId int
  --set @TalepId = 310

  -- Variables
  declare @GelecekAy Datetime
  declare @BugunDonemTipiId int = 0
  declare @GelecekDonemTipiId int = 0
  declare @bugun smalldatetime = getdate()

  declare @zamanlamaTipiId int = 0
  declare @isGrupSubeYok bit = 0
  declare @YasSiniri smallint = 0
  declare @yasOnayiTipiId smallint = 0
  declare @yasHazirTarihi date = null
  declare @deneyimSartiTipiId smallInt = 0
  declare @sertifikaTeslimEdildi bit = 0

  -- MtskAdayTalep
  declare @yFirmId Int 
  declare @yTalepTipiId SmallInt 
  declare @yKayitTipiId SmallInt 
  declare @yMebbisKurumOnayi Bit 
  declare @yMebbisDurumTipiId SmallInt
  declare @yMevcutSertifikaTipiId smallInt = 0
  declare @yIstenenSertifikaTipiId smallInt = 0
  declare @yDonemTipiId int = 0
  declare @yGrupTipiId smallint = 0
  declare @ySubeTipiId smallint = 0
  declare @yBelgelerGeldi bit = 0

  declare @ySistemUyarilari nvarchar(200)
  declare @oldSistemUyarilari nvarchar(200)
  declare @yeniSistemUyarisi nvarchar(200)
  declare @grupSubeSil bit = 0

  set @GelecekAy = DATEADD(month, 1, getdate());
	
  -- Bugün hangi dönem ?  tespit ediliyor
  if (MONTH(getdate()) < 10)
       set @BugunDonemTipiId = convert(int, convert(varchar(5), YEAR(getdate())) + '0' + convert(varchar(3), MONTH(getdate())))
  else set @BugunDonemTipiId = convert(int, convert(varchar(5), YEAR(getdate())) + convert(varchar(3), MONTH(getdate())))

  if (MONTH(@GelecekAy) < 10)
       set @GelecekDonemTipiId = convert(int, convert(varchar(5), YEAR(@GelecekAy)) + '0' + convert(varchar(3), MONTH(@GelecekAy)))
  else set @GelecekDonemTipiId = convert(int, convert(varchar(5), YEAR(@GelecekAy)) + convert(varchar(3), MONTH(@GelecekAy)))

  Select 
       @yFirmId = ins.FirmId
      ,@yTalepTipiId = isnull(ins.TalepTipiId,0)
      ,@yKayitTipiId = isnull(ins.KayitTipiId,0)
      ,@yMebbisKurumOnayi = isnull(ins.MebbisKurumOnayi,0)
      ,@yMebbisDurumTipiId = isnull(ins.MebbisDurumTipiId,0)
	  ,@yMevcutSertifikaTipiId = isnull(ins.MevcutSertifikaTipiId,0)
      ,@yIstenenSertifikaTipiId = isnull(ins.IstenenSertifikaTipiId,0)
      ,@yDonemTipiId = isnull(ins.DonemTipiId,0)
      ,@yGrupTipiId = isnull(ins.GrupTipiId,0)
      ,@ySubeTipiId = isnull(ins.SubeTipiId,0)
      ,@yBelgelerGeldi = isnull(ins.BelgelerGeldi,0)
      ,@ySistemUyarilari = isnull(ins.SistemUyarilari,'')
  From  dbo.MtskAdayTalep as ins, dbo.MtskAday as aday
  Where ins.AdayId = aday.Id
  and   ins.Id = @TalepId

  -- ----------------------------------------------------------------------------------
  -- Aday kayıt aşaması
  -- ----------------------------------------------------------------------------------
  Select @sertifikaTeslimEdildi = isnull(sertifika.SertifikaTeslimEdildi,0)
  From dbo.MtskAdaySertifika as sertifika
  Where sertifika.TalepId = @TalepId

  -- ZamanlamaTipiId 
  -- Aktif eğitim süreci devam edenler için geçerli
  -- eğitimi bitenleri -1 yapacağız
  if (@yDonemTipiId = @BugunDonemTipiId) set @zamanlamaTipiId = 2
  if (@yDonemTipiId = @GelecekDonemTipiId) set @zamanlamaTipiId = 3
  if (@yDonemTipiId > @GelecekDonemTipiId) set @zamanlamaTipiId = 4
  if (@yDonemTipiId = 0) set @zamanlamaTipiId = 0
  if (@yDonemTipiId < @BugunDonemTipiId and @sertifikaTeslimEdildi = 0) set @zamanlamaTipiId = 1
  if (@yDonemTipiId < @BugunDonemTipiId and @sertifikaTeslimEdildi = 1) set @zamanlamaTipiId = 5
  -- 

  -- IsGrupSubeYok 
  if (@yGrupTipiId = 0 or @ySubeTipiId = 0)
      set @isGrupSubeYok = 1
  
  -- DeneyimSartiTipiId
  declare @isEnAz bit
  declare @deneyimSertifikaTipiId smallInt = 0

  Select top 1 @isEnAz = isnull(saat.IsEnAz,0), @deneyimSertifikaTipiId = isnull(saat.DeneyimSertifikaTipiId,0) 
  From MtskSaatUygulama as saat
       left outer join GecerlilikTarihi as gt on ( saat.GecerlilikTarihiId = gt.Id )
  Where saat.IstenenSertifikaTipiId = @yIstenenSertifikaTipiId
  and   gt.KonuTipiId = 21102 -- Uygulama saatleri
  order by gt.Tarih desc
  
  set @deneyimSartiTipiId = [dbo].[fnc_getSertifikaDeneyimSarti] ( @TalepId )

  -- [Lkp].[MtskAdayTakipDeneyimSartiTipi] 
  -- 1, 'Deneyim şartı yok'
  -- 2, 'Deneyim şartına uygun'
  -- 3, 'Deneyim şartına uygun değil'
  set @yeniSistemUyarisi = ''
  if (@deneyimSartiTipiId = 3) 
  begin
      set @yeniSistemUyarisi = 'Deneyim şartı uygun değil'
	  set @grupSubeSil = 1
  end else
  begin
      if (CHARINDEX('Deneyim şartı uygun değil', @ySistemUyarilari) > 0) set @yeniSistemUyarisi = 'uyarıyıSil'
	  set @grupSubeSil = 0
  end

  if (@yeniSistemUyarisi != '')
  begin 
    set @oldSistemUyarilari = @ySistemUyarilari
    if (CHARINDEX('Deneyim şartı', @yeniSistemUyarisi) > 0)
    begin
	   -- @ySistemUyarilari boş değilse
       if (CHARINDEX('Deneyim şartı', @ySistemUyarilari) = 0 and len(@ySistemUyarilari) > 0) set @ySistemUyarilari = @yeniSistemUyarisi + CHAR(13) + CHAR(10) + @ySistemUyarilari
	   -- @ySistemUyarilari boş ise
	   if (CHARINDEX('Deneyim şartı', @ySistemUyarilari) = 0 and len(@ySistemUyarilari) = 0) set @ySistemUyarilari = @yeniSistemUyarisi
    end 
    if (CHARINDEX('uyarıyıSil', @yeniSistemUyarisi) > 0)
    begin
       declare @i1 int = 0
       declare @i2 int = 0
       set @i1 = charindex('Deneyim şartı', @ySistemUyarilari) 
       set @i2 = 26-- Deneyim şartı uygun değil
       if (@i1 > 0)
           set @ySistemUyarilari = trim(stuff(@ySistemUyarilari, @i1, @i2, ''))
    end
	if (@oldSistemUyarilari <> @ySistemUyarilari)
    begin
	    Update dbo.MtskAdayTalep set SistemUyarilari = @ySistemUyarilari
        Where Id = @TalepId
    end
  end

  if (@grupSubeSil = 1)
  begin
      Update dbo.MtskAdayTalep set GrupTipiId = 0, SubeTipiId = 0
      Where Id = @TalepId
  end

  -- ----------------------------------------------------------------------------------
  -- Teorik Dersler aşaması
  -- TeorikBaslamaTarihi    
  -- ToerikBitisTarihi      
  -- TeorikDersTipiId       
  -- ----------------------------------------------------------------------------------

  declare @TeorikBaslamaTarihi date   
  declare @TeorikBitisTarihi date
  declare @TeorikDersTakipTipi smallInt 
  
  Select 
      @TeorikBaslamaTarihi = gs.GrupBaslamaTarihi
    , @TeorikBitisTarihi   = gs.GrupBitisTarihi
    , @TeorikDersTakipTipi = gs.TakipTipiId
  From MtskGrupSubesi as gs
  Where gs.FirmId      = @yFirmId
  and   gs.DonemTipiId = @yDonemTipiId
  and   gs.GrupTipiId  = @yGrupTipiId
  and   gs.SubeTipiId  = @ySubeTipiId

  -- Mevcut ehliyeti var ise
  -- Talep tipi Sertifika değil ise
  if (@yMevcutSertifikaTipiId > 0) or
     (@yTalepTipiId <> 1)
  begin
      set @TeorikBaslamaTarihi = null
      set @TeorikBitisTarihi   = null
      set @TeorikDersTakipTipi = 6 -- Muaf işareti
  end


  -- Tespitleri kaydet
  Update MtskAdayTakip set
     -- Kayıt takibi
       TalepTipiId            = @yTalepTipiId  
     , DonemTipiId            = @yDonemTipiId
     , GrupTipiId             = @yGrupTipiId
     , SubeTipiId            = @ySubeTipiId
     , KayitTipiId            = @yKayitTipiId
     , ZamanlamaTipiId        = @zamanlamaTipiId
     , IsGrupSubeYok          = @isGrupSubeYok
     , MebbisKurumOnayi       = @yMebbisKurumOnayi
     , MebbisDurumTipiId      = @yMebbisDurumTipiId
     , DeneyimSartiTipiId     = @deneyimSartiTipiId
     , IsDeneyimEnAz          = @isEnAz
     , DeneyimSertifikaTipiId = @deneyimSertifikaTipiId
	 , BelgelerGeldi          = @yBelgelerGeldi
	 -- Teorik ders takibi
     , TeorikBaslamaTarihi = @TeorikBaslamaTarihi
     , TeorikBitisTarihi   = @TeorikBitisTarihi  
     , TeorikDersTipiId    = @TeorikDersTakipTipi 
  Where TalepId = @TalepId

    -- -------------------------------------------------------------------------
    -- eSinav sonuçları
    -- -------------------------------------------------------------------------
    -- DİKKAT : Aynı kod prc_MtskAdayTakip içinde var	
    ----------------------------------------------------------------------------
    update MtskAdayTakip set
	  eSinavNo = x.SinavNoYeni
    , eSinavDurumTipiId = x.eSinavDurumTipiId
    , eSinavNoGirdigi = x.SinavNoGirdigi
    , eSinavSonucTipiId = x.DurumuTipiId
    , eSinavSonrasiTipiId = x.eSinavSonrasiTipiId
    , eSinavMuracatTarihi = x.eSinavMuracatTarihi
	, eSinavDetayId = x.sinavId
	, eSinavBasariTarihi = x.SinavTarihi
	, UygulamaliDersTipiId = x.UygulamaliDersTipiId
    from MtskAdayTakip a, 
    (
	Select 
	       eSinav.Id as sinavId
	     , eSinav.FirmId
         , eSinav.TalepId
	     , eSinav.AdayId 
	     , eSinav.SinavNo as SinavNoGirdigi
         , eSinav.DurumuTipiId
		 , convert(date, eSinav.SinavTarihi, 103) as SinavTarihi

    -- eSinavDurumTipiId
    --   0 : ''
    --   1 : e-Sınav için hazır değil
    --   2 : e-Sınav başvurusu yapılacak
    --   3 : e-Sınav başvurusu yapıldı
    --   4 : e-Sınav sonucu okunacak
    --   5 : e-Sınav sonucu okundu
	--   6 : Muaf

	--   eSinav.DurumuTipi
	--   1   Başarılı
	--   2   Başarısız
	--   3   Girmedi
	--   4   Raporlu
	--   5   İptal Edildi(Sınav Yasaklı)

	     , case
             when (eSinav.DurumuTipiId = 0 and convert(date, @bugun,103) <  convert(date, eSinav.SinavTarihi, 103)) then 3 -- e-Sınav başvurusu yapıldı
             when (eSinav.DurumuTipiId = 0 and convert(date, @bugun,103) >= convert(date, eSinav.SinavTarihi, 103)) then 4 -- e-Sınav sonucu okunacak
	         when (eSinav.DurumuTipiId = 1) then 5 -- e-Sınav sonucu okundu < sınav başarılıysa
	         when (eSinav.DurumuTipiId > 1 and eSinav.SinavNo < 4) then 2 -- e-Sınav başvurusu yapılacak < 1,2,3 sınavlarda başarılı değilse 
	         when (eSinav.DurumuTipiId > 1 and eSinav.SinavNo = 4) then 5 -- e-Sınav sonucu okundu       < 4. sınavda başarılı değilse
           end as eSinavDurumTipiId
	     , case
	         when (eSinav.DurumuTipiId = 1) then 0 -- < sınav başarılıysa
	         when (eSinav.DurumuTipiId > 1 and eSinav.SinavNo < 4) then (eSinav.SinavNo + 1) --  < 1,2,3 sınavlarda başarılı değilse
	         when (eSinav.DurumuTipiId > 1 and eSinav.SinavNo = 4) then 0 --  < 4. sınavda başarılı değilse
           end as SinavNoYeni
         , case
	         when (eSinav.DurumuTipiId = 1) then 2 -- Uygulama aşamasına geçti
             when (eSinav.DurumuTipiId > 1) and (eSinav.SinavNo < 4 ) then 1  -- Tekrar sınava girecek
             when (eSinav.DurumuTipiId > 1) and (eSinav.SinavNo >= 4 ) then 3 -- Tüm e-Sınav hakkı bitti
           end as eSinavSonrasiTipiId
         , case 
	         when (eSinav.DurumuTipiId > 1) and (eSinav.SinavNo < 4 ) then (
                  case when DAY(eSinav.SinavTarihi) >= 1 and DAY(eSinav.SinavTarihi) <= 15 then DATEADD(DAY, 16 - DAY(eSinav.SinavTarihi), eSinav.SinavTarihi)
                  else DATEADD(DAY, 1, EOMONTH(eSinav.SinavTarihi)) end
			 )
             else null
	       end as eSinavMuracatTarihi
         , case
	         when (eSinav.DurumuTipiId = 1 ) then 1   -- eSinav.Başarılı ise Uygulamalı dersler planlanacak
             when (eSinav.DurumuTipiId <> 1) then 0   -- boşluk
           end as UygulamaliDersTipiId

         from MtskSinavETeorik as eSinav,
	     (     -- Adayların son sınavlarını buluyor
	           Select TalepId, AdayId
			   , max(convert(smallDatetime,SinavTarihi,103)) as SinavTarihi
               from MtskSinavETeorik
		       Where FirmId = @yFirmId
			   and IsActive = 1
               -- ----------------------------------------
			   and TalepId = @TalepId -- Bu procedure özel
               -- ----------------------------------------
               group by TalepId, AdayId
         ) as aday
	     Where eSinav.TalepId = aday.TalepId
		 and   eSinav.AdayId = aday.AdayId
	     and   convert(smallDatetime,eSinav.SinavTarihi,103) = aday.SinavTarihi
    ) as x
    Where a.FirmId  = x.FirmId
    and   a.TalepId = x.TalepId
	and   a.AdayId  = x.AdayId 
    and (
       isnull(a.eSinavDurumTipiId,0)   <> x.eSinavDurumTipiId
    or isnull(a.eSinavSonucTipiId,0)   <> x.DurumuTipiId
    or isnull(a.eSinavSonrasiTipiId,0) <> x.eSinavSonrasiTipiId
	or isnull(a.eSinavDetayId,0)       <> x.sinavId
    or a.eSinavMuracatTarihi           <> x.eSinavMuracatTarihi
	or a.eSinavBasariTarihi            <> x.SinavTarihi
	--or a.UygulamaliDersTipiId <> x.UygulamaliDersTipiId : bu değişir
	)

	-- ---------------------------------------------------
    -- eSınavlar tamamen silinmiş olabilir
	-- ---------------------------------------------------
	
	update MtskAdayTakip set
      eSinavDurumTipiId = x.eSinavDurumTipiId
    , eSinavNo = 1
    , eSinavNoGirdigi = 0
    , eSinavSonucTipiId = 0
    , eSinavSonrasiTipiId = 0
    , eSinavMuracatTarihi = null
	, eSinavDetayId = 0
	, eSinavBasariTarihi = null
	, UygulamaliDersTipiId = 0
    from MtskAdayTakip a, 
    (
		 Select takip.Id as TakipId
        ,(case  
          when takip.TeorikDersTipiId = 17 then 2 -- e-Sınav başvurusu yapılacak
          else 1 end ) as eSinavDurumTipiId       -- e-Sınav için hazır değil
		 from dbo.MtskAdayTakip as takip
		     left outer join dbo.MtskSinavETeorik as sinav on ( 
			          takip.TalepId = sinav.TalepId 
                  -- ----------------------------------------
			      and sinav.TalepId = @TalepId -- Bu procedure özel
                  -- ----------------------------------------
			      and sinav.IsActive = 1 )
         Where takip.FirmId = @yFirmId
         and   takip.eSinavDurumTipiId > 0  
         and   takip.eSinavDurumTipiId <> 6 -- Muaf değilse
		 -- ----------------------------------------
         and takip.TalepId = @TalepId -- Bu procedure özel
         -- ----------------------------------------
		 Group by takip.Id
		        , takip.TeorikDersTipiId
		 Having COUNT(sinav.Id) = 0
     ) as x
	 Where a.Id = x.TakipId 



    -- -------------------------------------------------------------------------
    -- uSinav sonuçları
    -- -------------------------------------------------------------------------
    -- DİKKAT : Aynı kod prc_MtskAdayTakip içinde var	
    ----------------------------------------------------------------------------

    update MtskAdayTakip set
      uSinavNo = x.SinavNoYeni
    , uSinavDurumTipiId = x.uSinavDurumTipiId
    , uSinavNoGirdigi = x.SinavNoGirdigi
    , uSinavSonucTipiId = x.uSinavSonucTipiId
    , uSinavSonrasiTipiId = x.uSinavSonrasiTipiId
	, uSinavDetayId = x.sinavId
	, uSinavBasariTarihi = x.SinavTarihi --x.uSinavBasariTarihi
	, BasarisizDersTipiId = x.BasarisizDersTipiId

    from MtskAdayTakip a, 
    (
         Select 
	       uSinav.Id as sinavId
	     , uSinav.FirmId
         , uSinav.TalepId
	     , uSinav.AdayId 
	     , uSinav.SinavNo as SinavNoGirdigi
         , uSinav.DurumuTipiId
		 , convert(date, uSinav.SinavTarihi, 103) as SinavTarihi

	--   Id  MtskAdayTakipi.uSinavDurumTipi
	--   0   Tanımsız
	--   1   Henüz sınav aşamasında değil
	--   2   Uygulama sınavı için usta öğretici onayı isteniyor
	--   3   Uygulama sınavı için hazır değil
	--   4   Uygulama sınavı için MEBBİS de onaylanacak
	--   5   Uygulama sınav randevusu planlanacak
	--   6   Uygulama sınav randevusu MEBBİSe aktarılacak
	--   7   Uygulama sınavına girecek
	--   8   Uygulama sınav sonucu okunacak
	--   9   Uygulama sınav sonucu okundu
	--   
	--   Id  MtskSinavUygulama.DurumuTipi
	--   0
	--   1   Uygulama Sınav Aşamasında
	--   2   2.Direksiyon Aşamasında
	--   3   Sertifika Almaya Hak Kazandı
	--   4   Uygulama Sınav Hakkı Doldu
	--   5   Sertifikası İptal Edildi
	--   6   Durumu Girilmemiş
    -- 
	--   Id  MtskSinavUygulama.PuanTipi
	--   -2  Durumu Girilmemiş
	--   -1  Girmedi
	--   0   
	--   1   Başarısız
	--   100 Başarılı

	     , ( case
             when (uSinav.PuanTipiId = 0 and convert(date, @bugun,103) <  convert(date, uSinav.SinavTarihi, 103)) then 7 -- Uygulama sınavına girecek
             when (uSinav.PuanTipiId = 0 and convert(date, @bugun,103) >= convert(date, uSinav.SinavTarihi, 103)) then 8 -- Uygulama sınav sonucu okunacak
	         when (uSinav.PuanTipiId = 100) then 9 -- Uygulama sınav sonucu okundu < sınav başarılıysa
	         when ((uSinav.PuanTipiId = 1 or uSinav.PuanTipiId = -1) and uSinav.SinavNo < 4) then 5 -- Uygulama sınav randevusu planlanacak < 1,2,3 sınavlarda başarılı değilse 
	         when ((uSinav.PuanTipiId = 1 or uSinav.PuanTipiId = -1) and uSinav.SinavNo = 4) then 9 -- Uygulama sınav sonucu okundu         < 4. sınavda başarılı değilse
             end ) as uSinavDurumTipiId
	     , case
	         when (uSinav.PuanTipiId = 100) then 0 -- < sınav başarılıysa
	         when ((uSinav.PuanTipiId = 1 or uSinav.PuanTipiId = -1) and uSinav.SinavNo < 4) then (uSinav.SinavNo + 1) --  < 1,2,3 sınavlarda başarılı değilse
	         when ((uSinav.PuanTipiId = 1 or uSinav.PuanTipiId = -1) and uSinav.SinavNo = 4) then 0 --  < 4. sınavda başarılı değilse
           end as SinavNoYeni


	     , ( case
			 when uSinav.PuanTipiId = 100 then 1 -- Başarılı
             when uSinav.PuanTipiId =   1 then 2 -- Başarısız
             when uSinav.PuanTipiId =  -1 then 3 -- Girmedi 
			 else 0 end ) as uSinavSonucTipiId
         , ( case
	         when (uSinav.PuanTipiId = 100) then 2    -- Sertifika aşamasına geçti
             when (uSinav.PuanTipiId = 1) and (uSinav.SinavNo  < 4 ) then 1  -- Tekrar sınava girecek
             when (uSinav.PuanTipiId = 1) and (uSinav.SinavNo >= 4 ) and (talep.TalepTipiId = 1)  then 3  -- sertifikaTalebi ve sınavdan başarısız ise   Tüm uygulamalı sınav hakkı bitti
             when (uSinav.PuanTipiId = 1) and (uSinav.SinavNo >= 4 ) and (talep.TalepTipiId = 2 or talep.TalepTipiId = 3)  then 4  -- 2.+4 veya nakil 2.+4  ve sınavdan başarısız ise   2. +4 sınav hakkı bitti
             end ) as uSinavSonrasiTipiId
         --, ( case 
	     --    when uSinav.PuanTipiId = 100 then uSinav.SinavTarihi 
         --    else null end ) as uSinavBasariTarihi
         , ( case
	         when (uSinav.PuanTipiId = 100) then 0    -- tanımsız, başarısız eğitim planlanmayacak
             when (uSinav.PuanTipiId = 1) and (uSinav.SinavNo  < 4 ) then 1  -- 2 saat başarısız aday eğitimi planlanacak
             when (uSinav.PuanTipiId = 1) and (uSinav.SinavNo >= 4 ) and (talep.TalepTipiId = 1)  then 0  -- sertifikaTalebi ve sınavdan başarısız ise   Tüm uygulamalı sınav hakkı bittiği için Tanımsız, başarısız eğitim planlanmayacak
             when (uSinav.PuanTipiId = 1) and (uSinav.SinavNo >= 4 ) and (talep.TalepTipiId = 2 or talep.TalepTipiId = 3)  then 0  -- 2.+4 veya nakil 2.+4  ve sınavdan başarısız ise 2. +4 sınav hakkı bittiği için Tanımsız, başarısız eğitim planlanmayacak
             when (uSinav.PuanTipiId = -1) then 6 -- Girmedi için Eğitime gerek yok (Sınava girmedi)
             end ) as BasarisizDersTipiId

         from MtskSinavUygulama as uSinav, dbo.MtskAdayTalep as talep,
	     (     -- Adayların son sınavlarını buluyor
	           Select TalepId, AdayId
			   , max(convert(smallDatetime,SinavTarihi,103)) as SinavTarihi
               from MtskSinavUygulama
		       Where FirmId = @yFirmId
			   and IsActive = 1
               -- ----------------------------------------
			   and TalepId = @TalepId -- Bu procedure özel
               -- ----------------------------------------

               group by TalepId, AdayId
         ) as aday
	     Where uSinav.TalepId = talep.Id
         and   uSinav.TalepId = aday.TalepId
		 and   uSinav.AdayId = aday.AdayId
	     and   convert(smallDatetime,uSinav.SinavTarihi,103) = aday.SinavTarihi
    ) as x
    Where a.FirmId  = x.FirmId
    and   a.TalepId = x.TalepId
	and   a.AdayId  = x.AdayId 
    and (
       isnull(a.uSinavNo,0)            <> x.SinavNoGirdigi
    or isnull(a.uSinavDurumTipiId,0)   <> x.uSinavDurumTipiId
    or isnull(a.uSinavSonucTipiId,0)   <> x.uSinavSonucTipiId
    or isnull(a.uSinavSonrasiTipiId,0) <> x.uSinavSonrasiTipiId
	or isnull(a.uSinavDetayId,0)       <> x.sinavId
	or a.uSinavBasariTarihi  <> x.SinavTarihi )


	-- ---------------------------------------------------
    -- uSınavlar tamamen silinmiş olabilir
	-- ---------------------------------------------------
    print 'u-Sınav sonuçları silinmişse'
	update MtskAdayTakip set
      uSinavDurumTipiId = x.uSinavDurumTipiId
    , uSinavNo = 1
    , uSinavNoGirdigi = 0
    , uSinavSonucTipiId = 0
    , uSinavSonrasiTipiId = 0
    --, uSinavMuracatTarihi = null
	, uSinavDetayId = 0
	, uSinavBasariTarihi = null
	, BasarisizDersTipiId = 0
    from MtskAdayTakip a, 
    (
	     -- Id   MtskAdayTakipi.UygulamaliDersTipi
         -- 5    Uygulamalı dersleri tamamladı

		 -- Id   MtskAdayTakipi.uSinavDurumTipi
		 -- 5    Uygulama sınav randevusu planlanacak

		 Select takip.Id as TakipId
        ,(case  
          when takip.UygulamaliDersTipiId = 5 then 5 -- Uygulama sınav randevusu planlanacak
          else 5 end ) as uSinavDurumTipiId          -- hangi aşamayı döndüreceğimi bilemedim, yine 5 yaptım
		 from dbo.MtskAdayTakip as takip
		     left outer join dbo.MtskSinavUygulama as sinav on ( 
			          takip.TalepId = sinav.TalepId 
                  -- ----------------------------------------
			      and sinav.TalepId = @TalepId -- Bu procedure özel
                  -- ----------------------------------------
			      and sinav.IsActive = 1 )
         Where takip.FirmId = @yFirmId
         and   takip.uSinavDurumTipiId > 0  
		 -- ----------------------------------------
         and takip.TalepId = @TalepId -- Bu procedure özel
         -- ----------------------------------------

		 Group by takip.Id
		        , takip.UygulamaliDersTipiId
		 Having COUNT(sinav.Id) = 0
     ) as x
	 Where a.Id = x.TakipId 
     and ( a.uSinavDurumTipiId <> x.uSinavDurumTipiId
        or a.uSinavSonucTipiId <> 0 )


END -- procedure

/*CreateProcedureEnd*/

/*CreateTable*/

begin transaction

 create table MtskSinavETeorik
 (
   Id                    Int IDENTITY(1,1) not null, 
   FirmId                Int       not null,
   IsActive              Bit       not null, /* default : 1 = active, 0 = not active */  
   TalepId               Int       not null, 
   AdayId                Int       not null, /* default 0 */
   TcNo                  Varchar(11)   null,
   TamAdiSoyadi          nVarchar(100) null,
   AdayDonemTipiId       Int           null, /* bu mebbisde e-sınav listelerinde yok, uyg. sınav listelerinde var */ 

   --10
   SinavDonemTipiId        Int           null, /* sınavın yapıldığı dönem */
   SinavNo                 SmallInt      null,  
   SinavTarihSaat          Varchar(20)   null,
   SinavTarihi             Date          null,
   SinavSaati              Time          null,
   TeorikPuani             SmallInt      null, 
   DurumuTipiId            SmallInt      null, /* mebbiste okunuyor*/
   MebbisESinavTarihValue  Varchar(20)   null, /* mebbis valuesi */ 

   --20
   SinavUcreti           Numeric(22,5) null,
   KayitSirasindaAlindi  Bit           null, /* sertifika ücreti ile birlikte alındı */
   TahsilEdildi          Bit           null, /* sonradan kursiyerden makbuzla tahsil edildi */
   DekontId              Int           null, /* tahsil dekont ıd */
   --30
   /* mebbis bilgileri */
   UcretDurumuTipiId     SmallInt      null, /* mebbisten okunuyor : Sınav Ücreti Yatırılmamış ,  */
   YatanToplamUcret      Numeric(22,5) null,
   UcretSayisi           SmallInt      null, 
   TestMerkeziBilgisi    Varchar(50)   null,

   constraint		pk_MtskSinavETeorik primary key (Id)
 )
  
 grant select, insert, update, delete on MtskSinavETeorik to public

 create index X_MtskSinavETeorik01 on MtskSinavETeorik (TalepId)
 create index X_MtskSinavETeorik02 on MtskSinavETeorik (AdayId)
 create index X_MtskSinavETeorik03 on MtskSinavETeorik (TcNo)
  
commit transaction

/*CreateTableEnd*/

/*CreateTrigger*/

CREATE TRIGGER [dbo].[trg_MtskSinavETeorik] on [dbo].[MtskSinavETeorik] 
AFTER INSERT, UPDATE
AS BEGIN

    DECLARE eSinavCursor cursor for select Id from Inserted
    DECLARE @curId bigint

    OPEN eSinavCursor

    FETCH NEXT FROM eSinavCursor INTO @curId
    
	declare @yFirmId int = 0
	declare @yAdayId int = 0
	declare @yTcNo varchar(11)
	declare @yTamAdiSoyadi nVarchar(100)
	declare @SinavDonemTipiId int = 0
	declare @ySinavNo int = 0
	declare @ySinavTarihSaat varchar(20) = ''
	declare @xSinavTarihSaat varchar(20) = ''
	declare @ySinavTarihi date = null
	declare @ySinavSaati time = null
	declare @yTeorikPuani int = 0
	declare @yMebbisESinavTarihValue varchar(20) = ''
	declare @SinavSonucTipiId smallInt = 0
	

    WHILE @@FETCH_STATUS = 0
    BEGIN
	    set @ySinavNo = 0 
	    set @ySinavTarihi = null
	    set @ySinavSaati = null
				
        select 
         @yFirmId = ins.FirmId
        ,@yAdayId = isnull(ins.AdayId,0)
		,@yTcNo = isnull(ins.TcNo,'')
		,@yTamAdiSoyadi = isnull(ins.TamAdiSoyadi,'')
		,@ySinavNo = isnull(ins.SinavNo,0)
		,@ySinavTarihSaat = isnull(ins.SinavTarihSaat, '0')
		,@yTeorikPuani = isnull(ins.TeorikPuani, 0)
		,@SinavSonucTipiId = isnull(ins.DurumuTipiId, 0)
		,@yMebbisESinavTarihValue = isnull(ins.MebbisESinavTarihValue,'')
		from inserted ins
    	where ins.Id = @curId
	
        if (@ySinavTarihSaat <> '0')
		begin
		  set @xSinavTarihSaat = replace(@ySinavTarihSaat, '/E-Sınav', '') 
		  set @ySinavTarihi = convert(date, @xSinavTarihSaat, 103)
		  set @ySinavSaati = convert(time, @xSinavTarihSaat, 103)
		end

		-- Sınavın hangi dönemde yapıldığı tespit ediliyor
		if (MONTH(@ySinavTarihi) < 10)
               set @SinavDonemTipiId = convert(int, convert(varchar(5), YEAR(@ySinavTarihi)) + '0' + convert(varchar(3), MONTH(@ySinavTarihi)))
          else set @SinavDonemTipiId = convert(int, convert(varchar(5), YEAR(@ySinavTarihi)) + convert(varchar(3), MONTH(@ySinavTarihi)))
		
		
		if (@yAdayId = 0) and (@yTcNo <> '')
		begin
		  Select top 1 @yAdayId = Id  From MtskAday
          Where TcNo = @yTcNo
          and   isnull(DonemTipiId,0) <= @SinavDonemTipiId
          order by DonemTipiId desc, Id desc

		  -- demekki aday hiç kayıtlı değil 
		  -- yani ilk defa MEBBİS in sınav sonuçları (uygulama) sayfasından okunuyor demek
		  if (@yAdayId = 0)
		  begin
              
			  INSERT INTO [dbo].[MtskAday]
                 ([FirmId]
                 ,[IsActive]
                 ,[TcNo]
                 ,[TamAdiSoyadi]
				 ,[DonemTipiId]
                 ,[MebbisKurumOnayi]
                 ,[KayitTipiId]
                 ,[KaynakKayitTipiId]
                 )
              VALUES
               (  @yFirmId --FirmId, int
                , 1        --IsActive, bit
                , @yTcNo   --TcNo, varchar(11)
                , @yTamAdiSoyadi --TamAdiSoyadi, nvarchar(100)
				, 0 -- @AdayDonemTipiId --DonemTipiId, int   << e-sınavda bu bilgi yok
                , 1        --MebbisKurumOnayi, bit
                , 2        --KayitTipiId
                , 3        --KaynakKayitTipiId 
               )
             
			  --Select @yAdayId = @@IDENTITY
			  Select @yAdayId = aday.Id
		      From MtskAday as aday
		      Where aday.FirmId = @yFirmId
		      and   aday.TcNo = @yTcNo 
		  end -- @yAdayId = 0
		end
		
		if (@yTeorikPuani >= 70) 
		   set @SinavSonucTipiId = 1 -- Başarılı
		
		if ((@yTeorikPuani > 0) and (@yTeorikPuani < 70)) 
		   set @SinavSonucTipiId = 2 -- Başarısız
		
		-- Sınav tarihinden 7 gün geçtiği halde okunmamışsa girmedi yap
		if ((@yTeorikPuani = 0) and (@ySinavTarihi < (GETDATE() - 7))) 
		   set @SinavSonucTipiId = 3 -- Girmedi
		
		Select @ySinavNo = (count(Id) + 1) From MtskSinavETeorik
		Where IsActive = 1
		and FirmId = @yFirmId
		and AdayId = @yAdayId
		and SinavTarihi < @ySinavTarihi

		update MtskSinavETeorik set
           AdayId = @yAdayId
		 , SinavDonemTipiId = @SinavDonemTipiId
		 , SinavNo = @ySinavNo
		 --, SinavTarihSaat = @ySinavTarihSaat
		 , SinavTarihi = @ySinavTarihi
         , SinavSaati = @ySinavSaati
		 , DurumuTipiId = @SinavSonucTipiId
		Where Id = @curId
	
	    -- İlk aldığımız datalarda tarihsaat yok
		-- sonrada tekrar mebbisten sınavsonuçaları okutulursa bu seferde aynı bilgi bu sefer tarihsaatli okunduğu için
		-- çift kayıt oluşuyor. Bu nedenle önceki saat bilgisi olmayan yani ilk datada okunan silinmesi gerekiyor
		if (@yMebbisESinavTarihValue <> '')
		begin
		  Delete From MtskSinavETeorik
		  Where SinavTarihi = @ySinavTarihi
		  and   isnull(MebbisESinavTarihValue,'') = ''
		  and   Id <> @curId 
		end

        FETCH NEXT FROM eSinavCursor INTO @curId
    END

	EXECUTE [dbo].[prc_MtskAdayTakip]  @yFirmId

    CLOSE eSinavCursor
    DEALLOCATE eSinavCursor   

END -- trigger

/*CreateTriggerEnd*/

ALTER TABLE [dbo].[MtskSinavETeorik] ENABLE TRIGGER [trg_MtskSinavETeorik]


/*CreateLkpTable*/

begin transaction

-- ------------------------------------------------------- 
-- DurumuTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'MtskSinavETeorikDurumuTipi')
drop table [Lkp].[MtskSinavETeorikDurumuTipi]

create table [Lkp].[MtskSinavETeorikDurumuTipi]
(
   Id               Int Unique not null, 
   DurumuTipi       nVarchar(20)   null
   
   constraint  pk_MtskSinavETeorikDurumuTipi primary key (Id)
)

Delete from [Lkp].[MtskSinavETeorikDurumuTipi]
INSERT INTO [Lkp].[MtskSinavETeorikDurumuTipi] (Id, DurumuTipi)
 VALUES 
  ( 0,    N''),
  ( 1,    N'Başarılı'),
  ( 2,    N'Başarısız'),
  ( 3,    N'Girmedi')

-- ------------------------------------------------------- 
-- UcretDurumuTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'MtskSinavETeorikUcretDurumuTipi')
drop table [Lkp].[MtskSinavETeorikUcretDurumuTipi]

create table [Lkp].[MtskSinavETeorikUcretDurumuTipi]
(
   Id               Int Unique not null, 
   UcretDurumuTipi  nVarchar(30)   null
   
   constraint  pk_MtskSinavETeorikUcretDurumuTipi primary key (Id)
)

Delete from [Lkp].[MtskSinavETeorikUcretDurumuTipi]
INSERT INTO [Lkp].[MtskSinavETeorikUcretDurumuTipi] (Id, UcretDurumuTipi)
 VALUES 
  ( 0,    N''),
  ( 1,    N'Sınav Ücreti Yatırılmış'),
  ( 2,    N'Sınav Ücreti Yatırılmamış')

commit transaction

/*CreateLkpTableEnd*/

/*CreateTable*/

begin transaction

 create table MtskAdayUcret
 (
   Id                    Int IDENTITY(1,1) not null, 
   FirmId                Int       not null,
   TalepId               Int       not null, 
   AdayId                Int       not null, 
   DonemTipiId           Int           null,
   TalepTipiId           SmallInt      null, /* = MtskAdayTalep.TalepTipi */

   -- 10
   /* Ücretlendirme bilgileri */
   IsUcretli               Bit           null, /* 0 = Ücretsiz, 1 = Ücretli */
   IsUcretsiz              Bit           null, /* 0 = Ücretsiz, 1 = Ücretli */
   IsLock                  Bit           null,

   -- 20
   SertifikaUcreti         Numeric(22,5) null, /* il bazlı sertifika ücreti - readOnly */
   BirSaatOzelDersUcreti   Numeric(22,5) null, /* Özel ders için */
   AlacagiOzelDersSayisi   SmallInt      null, /* Özel ders için */
   KontejanTipiId          SmallInt      null, /* şehit, gazi vb...  */
   KontejanIndirimOrani    Numeric(12,8) null, /* %25 indirim dersek : %75 aday öder, %25 kurs indirim yapmış olur */
   KontejanIndirimTutari   Numeric(22,5) null,
   KontejanSonrasiUcret    Numeric(22,5) null,
   
   -- 30
   AnlasilanUcret          Numeric(22,5) null,
   IskontoOrani            Numeric(12,8) null,
   PesinatTutari           Numeric(22,5) null, 
   TaksitSayisiTipiId      SmallInt      null, /* 0.Peşin, 1 Ay, 2 Ay, 3 Ay .... */
   TaksitlendirilecekUcret Numeric(22,5) null, /* AnlasilanUcret - PesinatTutari */
   -- 40
   ESinavUcreti            Numeric(22,5) null,
   UygulamaSinavUcreti     Numeric(22,5) null,
   SertifikaVeSinavUcreti  Numeric(22,5) null, /* AnlasilanUcret + ESinavUcreti + UygulamaSinavUcreti */
   IsSinavUcretiTaksitli   Bit           null, /* Taksitlendirirken sinav ücretlerini birleştir */
   -- 50
   OdemeTipiId             SmallInt      null, /* 1. Kredi Kartı, 2. Nakit, 3. Banka Havalesi, 4. Senet, 5. Açık Hesap */
   OdemeBaslamaTarihi      Date          null,
   OdemeCepTelefonu        nVarchar(15)  null,  
   SinavUcretTipiId        SmallInt      null, /* 1. Biz yatıracağız,  2. Adayın kendisi yatıracak */   

   -- 60 Mebbise bildirilecek ücretler
   MebbisSertifikaUcreti      Numeric(22,5) null,
   MebbisTeoDersSaati         SmallInt      null, 
   Mebbis1SaatTeoDersUcreti   Numeric(22,5) null, 
   MebbisUygDersSaati         SmallInt       null, 
   Mebbis1SaatUygDersUcreti   Numeric(22,5) null, 
      

   -- 70 Mevcut borçlar
   BorcSertifikaUcreti        Numeric(22,5) null,
   BorcESinavUcreti           Numeric(22,5) null,
   BorcUygulamaSinavUcreti    Numeric(22,5) null,
   BorcBasarisizEgitimUcreti  Numeric(22,5) null,
   BorcUcretlerToplami        Numeric(22,5) null,

   -- 80 Vadesi geçmiş olan borçlar
   VGSertifikaUcreti          Numeric(22,5) null,
   VGESinavUcreti             Numeric(22,5) null,
   VGUygulamaSinavUcreti      Numeric(22,5) null,
   VGBasarisizEgitimUcreti    Numeric(22,5) null,
   VGUcretlerToplami          Numeric(22,5) null,

   -- 90
   PersonelId              Int           null,
   TeklifTarihi            Datetime      null,
   VeriKaynakId            Int           null, /* Veri transferi edildiği tablodaki Id . Sorun olduğunda bilginin nereden geldiğine referans olsun */

   constraint		pk_MtskAdayUcret primary key (Id)
 )
  
 grant select, insert, update, delete on MtskAdayUcret to public

  create index X_MtskAdayUcret01 on MtskAdayUcret (TalepId)
  create index X_MtskAdayUcret02 on MtskAdayUcret (AdayId)
  create index X_MtskAdayUcret03 on MtskAdayUcret (DonemTipiId)

commit transaction

/*CreateTableEnd*/


/*CreateTrigger*/

CREATE TRIGGER dbo.trg_MtskAdayUcret on dbo.MtskAdayUcret
AFTER INSERT, UPDATE
AS BEGIN

    DECLARE ucretCursor cursor local for select Id from Inserted
    DECLARE @curId bigint

    OPEN ucretCursor
    FETCH NEXT FROM ucretCursor INTO @curId

    -- Veri Transferi başladımı
    declare @DbUpdatesIsActive Bit = 0
    set @DbUpdatesIsActive = [dbo].[fnc_getIsDbUpdate] (13)

    declare @TalepId int = 0
    declare @AdayId int = 0
    declare @FirmId int = 0
    declare @VeriKaynakId int = 0
    declare @KontejanTipiId smallint
	declare @IsKontejan bit = 0
	declare @SinavUcretTipiId smallInt 

    WHILE @@FETCH_STATUS = 0
    BEGIN
        select 
            @TalepId = ins.TalepId
           ,@AdayId = ins.AdayId
		   ,@VeriKaynakId = ins.VeriKaynakId
           ,@KontejanTipiId = ins.KontejanTipiId 
		   ,@SinavUcretTipiId = ins.SinavUcretTipiId
		from inserted ins
    	where ins.Id = @curId

        if (@VeriKaynakId > 0 and (@TalepId = 0 or @AdayId = 0))
        begin
            Update dbo.MtskAdayUcret set
                TalepId = t.Id  
              , AdayId  = t.AdayId 
              , TalepTipiId = t.TalepTipiId  
            From dbo.MtskAdayUcret as a,
                 dbo.MtskAdayTalep as t
            Where a.VeriKaynakId = t.VeriKaynakId				 
            and   a.Id = @curId
        end

        if (@DbUpdatesIsActive = 0)
        begin 
            if (@KontejanTipiId > 0) set @IsKontejan = 1 else set @IsKontejan = 0

            -- Takip
            Update MtskAdayTakip set
                IsKontejan = @IsKontejan
              , eSinavUcretTipiId = @SinavUcretTipiId 
              , uSinavUcretTipiId = @SinavUcretTipiId 
            Where TalepId = @TalepId
            and ( IsKontejan <> @IsKontejan 
             or   eSinavUcretTipiId <> @SinavUcretTipiId )  
        end		

        -- Data transferi oldu fakat daha önce bu talep için boş kayıt oluşturulmuş ise onu sil
        if (@VeriKaynakId > 0)
        begin
		    Delete From dbo.MtskAdayUcret
            Where TalepId = @TalepId
            and   isnull(VeriKaynakId,0) = 0
        end


        FETCH NEXT FROM ucretCursor INTO @curId
    END -- While

    CLOSE ucretCursor
    DEALLOCATE ucretCursor   

END -- create trigger

ALTER TABLE dbo.MtskAdayUcret ENABLE TRIGGER trg_MtskAdayUcret 

/*CreateTriggerEnd*/



/*CreateLkpTable*/

begin transaction


-- ------------------------------------------------------- 
-- TalepTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'MtskAdayUcretTalepTipi')
drop table [Lkp].[MtskAdayUcretTalepTipi]

create table [Lkp].[MtskAdayUcretTalepTipi]
(
   Id               Int Unique not null, 
   TalepTipi        nVarchar(50)   null,
   IsActive         Bit            null,
   
   constraint  pk_MtskAdayUcretTalepTipi primary key (Id)
)

Delete from [Lkp].[MtskAdayUcretTalepTipi]
INSERT INTO [Lkp].[MtskAdayUcretTalepTipi] (Id, TalepTipi, IsActive)
 VALUES 
  ( 0,    N'Seçiniz', 1),
  ( 1,    N'Sertifika', 1),
  ( 2,    N'2.+4 hak ', 1),
  ( 3,    N'Nakil 2.+4 hak', 1),
  ( 4,    N'100 Ceza', 1),
  ( 5,    N'Özel direksiyon ders (Aday değil)', 1),
  ( 6,    N'Özel teorik ders', 1)


-- ------------------------------------------------------- 
-- TaksitSayisiTipiId
-- ------------------------------------------------------- 

--IF EXISTS (SELECT * FROM sys.objects WHERE name = '')

IF EXISTS (Select * From sys.objects WHERE name = 'MtskAdayUcretTaksitSayisiTipi')
drop table [Lkp].[MtskAdayUcretTaksitSayisiTipi]

create table [Lkp].[MtskAdayUcretTaksitSayisiTipi]
(
   Id               Int Unique not null, 
   TaksitSayisiTipi nVarchar(20)   null,
   IsActive         Bit            null,
   
   constraint  pk_MtskAdayUcretTaksitSayisiTipi primary key (Id)
)

Delete from [Lkp].[MtskAdayUcretTaksitSayisiTipi]
INSERT INTO [Lkp].[MtskAdayUcretTaksitSayisiTipi] (Id, TaksitSayisiTipi, IsActive)
 VALUES 
  ( 0,    N'Seçiniz', 1),
  (100,   N'Peşin', 1),
  ( 1,    N'1 Ay', 1),
  ( 2,    N'2 Ay', 1),
  ( 3,    N'3 Ay', 1),
  ( 4,    N'4 Ay', 1),
  ( 5,    N'5 Ay', 1),
  ( 6,    N'6 Ay', 1),
  ( 7,    N'7 Ay', 1),
  ( 8,    N'8 Ay', 1),
  ( 9,    N'9 Ay', 1),
  (10,    N'10 Ay', 1),
  (11,    N'11 Ay', 1),
  (12,    N'12 Ay', 1)


-- ------------------------------------------------------- 
-- OdemeTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'MtskAdayUcretOdemeTipi')
drop table [Lkp].[MtskAdayUcretOdemeTipi]

create table [Lkp].[MtskAdayUcretOdemeTipi]
(
   Id               Int Unique not null, 
   OdemeTipi        nVarchar(20)   null,
   IsActive         Bit            null,
   
   constraint  pk_MtskAdayUcretOdemeTipi primary key (Id)
)

Delete from [Lkp].[MtskAdayUcretOdemeTipi]
INSERT INTO [Lkp].[MtskAdayUcretOdemeTipi] (Id, OdemeTipi, IsActive)
 VALUES 
  (-1,    N'', 1),
  ( 0,    N'Seçiniz', 1),
  ( 1,    N'Kredi Kartı', 1),
  ( 2,    N'Nakit', 1),
  ( 3,    N'Banka Havalesi', 1),
  ( 4,    N'Senet', 1),
  ( 5,    N'Açık Hesap', 1)


-- ------------------------------------------------------- 
-- SinavUcretTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'MtskAdayUcretSinavUcretTipi')
drop table [Lkp].[MtskAdayUcretSinavUcretTipi]

create table [Lkp].[MtskAdayUcretSinavUcretTipi]
(
   Id               Int Unique not null, 
   SinavUcretTipi   nVarchar(50)   null,
  
   constraint  pk_MtskAdayUcretSinavUcretTipi primary key (Id)
)

Delete from [Lkp].[MtskAdayUcretSinavUcretTipi]
INSERT INTO [Lkp].[MtskAdayUcretSinavUcretTipi] (Id, SinavUcretTipi)
 VALUES 
  ( 0,    N'Tanımsız'),
  ( 1,    N'Biz yatıracağız'),
  ( 2,    N'Adayın kendisi yatıracak')



commit transaction

/*CreateLkpTableEnd*/


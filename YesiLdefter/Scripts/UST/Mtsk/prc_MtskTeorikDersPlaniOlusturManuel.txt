/*CreateProcedure*/

CREATE PROCEDURE [dbo].[prc_MtskTeorikDersPlaniOlusturManuel] (@SablonTeorikFId int)  
AS BEGIN

  --declare @SablonTeorikFId int = 1

  declare @FirmId int
  declare @DonemTipiId int
  declare @GrupTipiId smallint
  declare @SubeTipiId smallint
  declare @BaslangicTarihi date
  declare @DerslikTipiId smallint
  declare @DerslikAdiTipiId smallint
  declare @DersSaatiTipiId smallint
  declare @SaatSayisi smallint
  declare @PersonelId varchar(20)
  declare @EgitimTipiId smallint
  declare @Count smallint

  Select  
	   @FirmId = [FirmId]
      ,@DonemTipiId = [DonemTipiId]
      ,@GrupTipiId = [GrupTipiId]
      ,@SubeTipiId = [SubeTipiId]
      ,@BaslangicTarihi = [BaslangicTarihi]
      ,@DerslikTipiId = [DerslikTipiId]
      ,@DerslikAdiTipiId = [DerslikAdiTipiId]
      ,@DersSaatiTipiId = [DersSaatiTipiId]
      ,@SaatSayisi = [SaatSayisi]
      ,@PersonelId = [PersonelId]
      ,@EgitimTipiId = [EgitimTipiId]
  From MtskSablonTeorikF
  Where Id = @SablonTeorikFId

  -- Mevcudu sil yeniden oluştursun
  Delete From dbo.MtskTeorikDers
  Where TDersIsUploaded = 0
  and   SablonTeorikBId = -2
  and   SablonTeorikFId = @SablonTeorikFId

  if (@DerslikAdiTipiId = 0)
  begin
    Select @DerslikAdiTipiId = DerslikAdiTipiId from MtskDerslik Where DerslikTipiId = @DerslikTipiId
    if (@DerslikTipiId = 5)
	    Select @DerslikAdiTipiId = DerslikAdiTipiId from MtskDerslik Where DerslikTipiId = 1
  end

  -- Bir ders günde Maximum 6 saat olabilir
  if (@SaatSayisi > 6) set @SaatSayisi = 6
  -- Bir ders günde Minimum 2 saat olabilir
  if (@SaatSayisi < 2) set @SaatSayisi = 2

  set @Count = 1
  WHILE ( @Count <= @SaatSayisi )
  BEGIN
      --PRINT 'The counter value is = ' + CONVERT(VARCHAR,@Count) + '; ' + CONVERT(varchar,@DersSaatiTipiId)
    if ( Select count(*) ADET from [dbo].[MtskTeorikDers]
         where FirmId = @FirmId
         and [DonemTipiId] = @DonemTipiId
         and [GrupTipiId] = @GrupTipiId
         and [SubeTipiId] = @SubeTipiId
         and [DerslikTipiId] = @DerslikTipiId
         and [DerslikAdiTipiId] = @DerslikAdiTipiId
         and [DersTarihi] = [dbo].fnc_IsGununuBul(@BaslangicTarihi, 1, 0, @DersSaatiTipiId + 107)
         and [DersSaatiTipiId] = @DersSaatiTipiId
	) = 0 
    begin
      Insert Into [dbo].[MtskTeorikDers]
           ([FirmId]
           ,[IsActive]
           ,[DonemTipiId]
           ,[GrupTipiId]
           ,[SubeTipiId]
           ,[DerslikTipiId]
           ,[DerslikAdiTipiId]
           ,[DersTarihi]
           ,[DersSaatiTipiId]
           ,[PersonelId]
           ,[EgitimTipiId]
           ,[BaslamaSaati]
           ,[BitisSaati]
           ,[TDersIsUploaded]
		   ,[SablonTeorikBId]
		   ,[SablonTeorikFId]
		   ,[OzelGunlerId]
		   )
      Values (@FirmId
           ,1
           ,@DonemTipiId
           ,@GrupTipiId
           ,@SubeTipiId
           ,@DerslikTipiId
           ,@DerslikAdiTipiId
           ,[dbo].fnc_IsGununuBul(@BaslangicTarihi, 1, 0, @DersSaatiTipiId + 107) -- 107 : Saatipi tanımı 107 den başlıyor
           ,@DersSaatiTipiId
           ,@PersonelId
           ,@EgitimTipiId
           ,null -- BaslamaSaati
           ,null -- BitisSaati
           ,0    -- TDersIsUploaded
           ,-2   -- SablonTeorikBId   : Default -2 değeri veriyorum, maunel ekranından ınsert olduğunun işareti
           ,@SablonTeorikFId
		   ,0    -- OzelGunlerId
           )
	end

	set @Count = @Count + 1
	set @DersSaatiTipiId = @DersSaatiTipiId + 1
	if (@DersSaatiTipiId > 15) set @Count = 100
  END

END -- procedure

/*CreateProcedureEnd*/
/*CreateTable*/

begin transaction

 create table MtskAdaySaglik
 (
   Id                    Int IDENTITY(1,1) not null, 
   FirmId                Int       not null,
   TalepId               Int       not null, 
   AdayId                Int       not null, 
   TcNo                  Varchar(11)   null,
   DonemTipiId           Int           null,
   VeriKaynakId          Int           null, /* Veri transferi edildiği tablodaki Id . Sorun olduğunda bilginin nereden geldiğine referans olsun */

   RaporReferansNo             nVarchar(50)   null,
   SaglikRaprunuVerenKurum     nVarchar(200)  null,
   SaglikRaporuSonuc           nVarchar(30)   null,
   SaglikRaporuTarihi          Date           null,
   SaglikRaporuSayisi          nVarchar(30)   null,
   OzurDurumuTipiId            SmallInt       null,
   OkutmanTalebi               Bit            null,
   TercumanTalebi              Bit            null,
   YabanciDilTipiId            SmallInt       null,
   SaglikRaporuResim           VarBinary(max) null,
   SaglikRaporuResimSmall      VarBinary(max) null,

   constraint		pk_MtskAdaySaglik primary key (Id)
 )
  
 grant select, insert, update, delete on MtskAdaySaglik to public

  create index X_MtskAdaySaglik01 on MtskAdaySaglik (TalepId)
  create index X_MtskAdaySaglik02 on MtskAdaySaglik (AdayId)
  create index X_MtskAdaySaglik03 on MtskAdaySaglik (TcNo)
  create index X_MtskAdaySaglik04 on MtskAdaySaglik (DonemTipiId)

commit transaction

/*CreateTableEnd*/


/*CreateTrigger*/

CREATE TRIGGER [dbo].[trg_MtskAdaySaglik] on [dbo].[MtskAdaySaglik]
AFTER INSERT, UPDATE
AS BEGIN

    DECLARE saglikCursor cursor local for select Id from Inserted
    DECLARE @saglikId bigint

    OPEN saglikCursor
    FETCH NEXT FROM saglikCursor INTO @saglikId

    -- Veri Transferi başladımı
    declare @DbUpdatesIsActive Bit = 0
    set @DbUpdatesIsActive = [dbo].[fnc_getIsDbUpdate] (13)

    Declare @TalepId int = 0
	declare @AdayId int
	declare @VeriKaynakId int
	declare @OzurDurumuTipiId smallInt = 0
    declare @OkutmanTalebi bit
    declare @TercumanTalebi bit
    declare @YabanciDilTipiId smallInt

    WHILE @@FETCH_STATUS = 0
    BEGIN

        select 
            @TalepId = ins.TalepId
           ,@AdayId = ins.AdayId
		   ,@VeriKaynakId = ins.VeriKaynakId
		   ,@OzurDurumuTipiId = isnull(ins.OzurDurumuTipiId,0)
           ,@OkutmanTalebi = isnull(ins.OkutmanTalebi,0)
           ,@TercumanTalebi = isnull(ins.TercumanTalebi,0)
           ,@YabanciDilTipiId = isnull(ins.YabanciDilTipiId,0)
		from inserted ins
    	where ins.Id = @saglikId
		
        if (@VeriKaynakId > 0 and (@TalepId = 0 or @AdayId = 0))
        begin
            Update dbo.MtskAdaySaglik set
                TalepId = t.Id  
               ,AdayId  = t.AdayId 
            From dbo.MtskAdaySaglik as a,
                 dbo.MtskAdayTalep as t
            Where a.VeriKaynakId = t.VeriKaynakId				 
            and   a.Id = @saglikId
        end

		
        if (@DbUpdatesIsActive = 0)
        begin 
            -- Takip
            Update MtskAdayTakip set
                OzurDurumuTipiId = @OzurDurumuTipiId
              , OkutmanTalebi = @OkutmanTalebi 
              , TercumanTalebi = @TercumanTalebi  
              , YabanciDilTipiId = @YabanciDilTipiId
            Where TalepId = @TalepId

            -- Talep tablosunu işaretle
            Update MtskAdayTalep set
                SaglikGeldi = x.Sonuc
            From MtskAdayTalep a, 
            (
               Select count(Id) as Sonuc
               From MtskAdaySaglik
               Where 0 = 0
               and ( SaglikRaporuResim is not null 
			   or Len(isnull(RaporReferansNo,'')) > 0
			   )
               and TalepId = @TalepId
            ) as x
            Where a.Id = @TalepId
        end		

		-- MtskAdayBelgeler tablosunu işaretle
		Update MtskAdayBelgeler set
        SaglikTarandi = x.Sonuc
        From MtskAdayBelgeler a, 
        (
            Select count(Id) as Sonuc
            From MtskAdaySaglik
            Where 0 = 0
            and (
			   SaglikRaporuResim is not null 
			or Len(isnull(RaporReferansNo,'')) > 0
			)
            and TalepId = @TalepId
        ) as x
        Where a.TalepId = @TalepId

        -- Data transferi oldu fakat daha önce bu talep için boş kayıt oluşturulmuş ise onu sil
        if (@VeriKaynakId > 0)
        begin
		    Delete From dbo.MtskAdaySaglik
            Where TalepId = @TalepId
            and   isnull(VeriKaynakId,0) = 0
        end
		
        FETCH NEXT FROM saglikCursor INTO @saglikId
    END -- While

    CLOSE saglikCursor
    DEALLOCATE saglikCursor   


END -- create trigger

/*CreateTriggerEnd*/

ALTER TABLE dbo.MtskAdaySaglik ENABLE TRIGGER trg_MtskAdaySaglik


/*CreateLkpTable*/

begin transaction

-- ------------------------------------------------------- 
-- OzurDurumuTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'MtskAdaySaglikOzurDurumuTipi')
drop table [Lkp].[MtskAdaySaglikOzurDurumuTipi]

create table [Lkp].[MtskAdaySaglikOzurDurumuTipi]
(
   Id               Int Unique Not    null,
   OzurDurumuTipi   nVarchar(60)   null

   constraint  pk_MtskAdaySaglikOzurDurumuTipi primary key (Id)
)

Delete from [Lkp].[MtskAdaySaglikOzurDurumuTipi]
INSERT INTO [Lkp].[MtskAdaySaglikOzurDurumuTipi] (Id, OzurDurumuTipi)
 VALUES 
  ( -1, N'--Seçiniz--'),
  (  1, N'Engeli Yok'),
  (100, N'Ortopedik Engelli (Ellerini ve/veya Kollarını Kullanamıyor)'),
  (110, N'Ortopedik Engelli (Yürüyemiyor)'),
  (120, N'İşitme, Dil veya Konuşma Engelli'),
  (140, N'Görme Engelli (Bir Gözü Görmüyor)'),
  (150, N'Görme Engelli (Büyüteç Yardımı İle Görebiliyor)'),
  (160, N'Süreğen hastalığı var.')



-- ------------------------------------------------------- 
-- YabanciDilTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'MtskAdaySaglikYabanciDilTipi')
drop table [Lkp].[MtskAdaySaglikYabanciDilTipi]

create table [Lkp].[MtskAdaySaglikYabanciDilTipi]
(
   Id               Int Unique Not    null,
   YabanciDilTipi   nVarchar(60)   null

   constraint  pk_MtskAdaySaglikYabanciDilTipi primary key (Id)
)

Delete from [Lkp].[MtskAdaySaglikYabanciDilTipi]
INSERT INTO [Lkp].[MtskAdaySaglikYabanciDilTipi] (Id, YabanciDilTipi)
 VALUES 
  ( -1, N'Yabancı Dil Seçiniz'),
  (  1, N'Arapça'),
  (  2, N'Çince'),
  (  3, N'İngilizce'),
  (  4, N'Almanca'),
  (  5, N'Fransızca'),
  (  6, N'Farsça'),
  (  7, N'Rusça')



commit transaction

/*CreateLkpTableEnd*/
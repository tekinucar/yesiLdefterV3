/*CreateTable*/

begin transaction

 create table MtskSinavRandevu
 (
   Id                       Int IDENTITY(1,1) not null, 
   ParentId                 Int       not null,
   FirmId                   Int       not null,
   IsActive                 Bit       not null, /* default : 1 = active, 0 = not active */  
   IsMasterRow              Bit           null, /* planlama hazırlanırken master row görevi olan satır, diğer satırlar bu master rowa göre çoğalıyor */
   Tarih                    Date          null,   
   MebbisRandevuTarihValue  Varchar(20)   null,
   GuzergahId               Int           null, /* sınavın yapılacağı güzergah */
   --10
   EgitimAraciId            Varchar(10)   null,
   PersonelNoTipiId         SmallInt      null, /* 1,2 veya 3 şeçince kullanıcı ona göre master row üzerindeki usta öğretici o kursiyere atanacak */ 
   PersonelId1              Varchar(20)   null,
   PersonelId2              Varchar(20)   null,
   PersonelId3              Varchar(20)   null,
   TalepId                  Int           null,
   AdayId                   Int           null,
   --20
   SinavNo                  SmallInt      null,     
   SaatTipiId               SmallInt      null,
   KullaniciUyarilari       nVarchar(200) null,
   IsGBelgesiHazir          Bit           null,
   IsGBelgesiTeslimEdildi   Bit           null,
   IsMebbisUploaded         Bit           null,

   constraint		pk_MtskSinavRandevu primary key (Id)
 )
  
 grant select, insert, update, delete on MtskSinavRandevu to public

 create index X_MtskSinavRandevu01 on MtskSinavRandevu (Tarih, EgitimAraciId)
 
commit transaction

/*CreateTableEnd*/


/*CreateTrigger*/

CREATE TRIGGER [dbo].[trg_MtskSinavRandevu] on [dbo].[MtskSinavRandevu] 
AFTER INSERT,UPDATE
AS 
BEGIN

    DECLARE randevuCursor cursor local for select Id from Inserted 
    DECLARE @curId bigint

    OPEN randevuCursor
    FETCH NEXT FROM randevuCursor INTO @curId

    declare @FirmId int
    declare @IsMasterRow Bit
    declare @Tarih Date
    declare @MebbisRandevuTarihValue  Varchar(20)
    declare @EgitimAraciId Varchar(10)
    declare @PersonelNoTipiId SmallInt
    declare @PersonelId0 Varchar(20)
    declare @PersonelId1 Varchar(20)
    declare @PersonelId2 Varchar(20)
    declare @PersonelId3 Varchar(20)
    declare @SaatTipiId SmallInt
	declare @TalepId int 
	declare @AdayId int
    declare @SinavNo SmallInt
	declare @IsMebbisUploaded bit

    declare @masterPersonelId1 Varchar(20)
    declare @masterPersonelId2 Varchar(20)
    declare @masterPersonelId3 Varchar(20)
	declare @oncekiSaatTipiId SmallInt
    
	
	WHILE @@FETCH_STATUS = 0
    BEGIN
	    select
            @FirmId = isnull(ins.FirmId,0)
           ,@IsMasterRow = isnull(ins.IsMasterRow,0)
           ,@Tarih = ins.Tarih
           ,@MebbisRandevuTarihValue = isnull(ins.MebbisRandevuTarihValue,'')
           ,@EgitimAraciId = isnull(ins.EgitimAraciId,'')
           ,@PersonelNoTipiId = isnull(ins.PersonelNoTipiId,0)
           ,@PersonelId1 = isnull(ins.PersonelId1,'')
           ,@PersonelId2 = isnull(ins.PersonelId2,'')
           ,@PersonelId3 = isnull(ins.PersonelId3,'')
           ,@SaatTipiId  = isnull(ins.SaatTipiId,0)
           ,@TalepId = isnull(ins.TalepId,0)
           ,@AdayId = isnull(ins.AdayId,0)
           ,@SinavNo = isnull(ins.SinavNo,0)
           ,@IsMebbisUploaded = isnull(ins.IsMebbisUploaded,0)
		From inserted ins
        Where ins.Id = @curId

		-- Detay satırı ise
		if (@IsMasterRow = 0)
        begin
            Select 
                @masterPersonelId1 = isnull(PersonelId1,'')
               ,@masterPersonelId2 = isnull(PersonelId2,'')
               ,@masterPersonelId3 = isnull(PersonelId3,'')
            From dbo.MtskSinavRandevu
            Where IsMasterRow = 1
            and   Tarih = @Tarih
            and   EgitimAraciId = @EgitimAraciId

			if (@PersonelNoTipiId = 0) set @PersonelNoTipiId = 1
			if (@PersonelNoTipiId = 1) set @PersonelId0 = @masterPersonelId1
			if (@PersonelNoTipiId = 2) set @PersonelId0 = @masterPersonelId2
			if (@PersonelNoTipiId = 3) set @PersonelId0 = @masterPersonelId3

            if (@SaatTipiId = 0)
            begin
                Select 
                    @oncekiSaatTipiId = isnull(max(SaatTipiId),0)
                From dbo.MtskSinavRandevu
                Where IsMasterRow = 0
                and   Tarih = @Tarih
                and   EgitimAraciId = @EgitimAraciId
                and   Id < @curId

				if (@oncekiSaatTipiId >= 1) set @oncekiSaatTipiId = @oncekiSaatTipiId + 1
				if (@oncekiSaatTipiId = 0) set @oncekiSaatTipiId = 1
				if (@oncekiSaatTipiId > 12) set @oncekiSaatTipiId = 0
            end
			else 
			   Set @oncekiSaatTipiId = @SaatTipiId

            select top 1 @SinavNo =
            ( case when  isnull(uSinavSonuc.Id,0) = 0 then 1 -- ilk sinav 
                   when (isnull(uSinavSonuc.PuanTipiId,0) = 1 or isnull(uSinavSonuc.PuanTipiId,0) = -1) and isnull(uSinavSonuc.SinavNo,0) < 4 then uSinavSonuc.SinavNo + 1
                   when (isnull(uSinavSonuc.PuanTipiId,0) = 1 or isnull(uSinavSonuc.PuanTipiId,0) = -1) and isnull(uSinavSonuc.SinavNo,0) = 4 then 0
                   when  isnull(uSinavSonuc.PuanTipiId,0) = 100 then 0
            end )
            from MtskAdayTalep as talep 
			   left outer join MtskSinavUygulama as uSinavSonuc on (talep.Id = uSinavSonuc.TalepId and uSinavSonuc.SinavTarihi < @Tarih)
            where talep.Id = @TalepId
            
            order by uSinavSonuc.SinavNo desc

			-- seçili noya göre personel value set oluyor
			Update dbo.MtskSinavRandevu set
                PersonelNoTipiId = @PersonelNoTipiId 
              , PersonelId1 = @PersonelId0
              , SaatTipiId = @oncekiSaatTipiId
              , SinavNo = @SinavNo
			Where Id = @curId

            if (@TalepId > 0)
            begin
                -- MtskAdayTakip 
                Update dbo.MtskAdayTakip set
                     IsRandevuPlanlandi = 1
                    ,IsRandevuMebbis = @IsMebbisUploaded
                    ,RandevuTarihi = @Tarih
                    ,RandevuSaatTipiId = @oncekiSaatTipiId
                    ,RandevuEgitimAraciId = @EgitimAraciId
                    ,RandevuPersonelId = @PersonelId0
                Where TalepId = @TalepId
            end

        end -- @IsMasterRow = 0

        if (@IsMasterRow = 1)
        begin
            declare @adet int 
            Select @adet = isnull(count(Id),0)
            From dbo.MtskSinavRandevu
            Where IsMasterRow = 1
            and   Tarih = @Tarih
            and   EgitimAraciId = @EgitimAraciId
			
			-- aynı gün içinde ikinci defa aynı araç için plan oluşturulamaz
			if (@adet > 1)
            begin
			    Update dbo.MtskSinavRandevu set 
				   EgitimAraciId = ''
                Where Id = @curId
            end --

        end -- @IsMasterRow = 1

        FETCH NEXT FROM randevuCursor INTO @curId
    END -- While

	--EXECUTE [dbo].[prc_MtskAdayTakip] @FirmId

    CLOSE randevuCursor
    DEALLOCATE randevuCursor  

END

/*CreateTriggerEnd*/


/*CreateTrigger*/

CREATE TRIGGER [dbo].[trg_MtskSinavRandevuDeleteIsLock] on [dbo].[MtskSinavRandevu]
INSTEAD of DELETE
AS BEGIN

    declare isLockCursorDlt cursor local for select Id from deleted
    declare @curId bigint

    OPEN isLockCursorDlt

    FETCH NEXT FROM isLockCursorDlt INTO @curId


    WHILE @@FETCH_STATUS = 0
    BEGIN
	    IF EXISTS (SELECT 1 FROM deleted WHERE isnull(IsMebbisUploaded,0) = 1 and Id = @curId)
        BEGIN
            PRINT 'Bu kayıt silinemez!'
            RETURN
        END

        Delete from dbo.MtskSinavRandevu  Where Id = @curId and isnull(IsMebbisUploaded,0) = 0

      FETCH NEXT FROM isLockCursorDlt INTO @curId
    END

    CLOSE isLockCursorDlt
    DEALLOCATE isLockCursorDlt

END

 /*CreateTriggerEnd*/



/*CreateTrigger*/

CREATE TRIGGER [dbo].[trg_MtskSinavRandevuDelete] on [dbo].[MtskSinavRandevu]
AFTER DELETE
AS BEGIN

    declare randevuCursorDlt cursor local for select Id from deleted
	declare @curId bigint

	OPEN randevuCursorDlt
    FETCH NEXT FROM randevuCursorDlt INTO @curId

    declare @TalepId int

    WHILE @@FETCH_STATUS = 0
    BEGIN
	    Select @TalepId = dlt.TalepId
	    From deleted dlt
	    Where dlt.Id = @curId
	  
        if (@TalepId > 0)
        begin
            -- MtskAdayTakip 
            Update dbo.MtskAdayTakip set
                IsRandevuPlanlandi = 0
               ,RandevuTarihi = null
               ,RandevuSaatTipiId = null
               ,RandevuEgitimAraciId = null
               ,RandevuPersonelId = null
               ,uSinavDurumTipiId = 5 -- Uygulama sınav randevusu planlanacak
            Where TalepId = @TalepId
        end	    

        FETCH NEXT FROM randevuCursorDlt INTO @curId
    END

    CLOSE randevuCursorDlt
    DEALLOCATE randevuCursorDlt

END

/*CreateTriggerEnd*/





/*CreateLkpTable*/

begin transaction

-- ------------------------------------------------------- 
-- SaatTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'MtskSinavRandevuSaatTipi')
drop table [Lkp].[MtskSinavRandevuSaatTipi]

create table Lkp.MtskSinavRandevuSaatTipi
(
   Id              Int Unique Not Null,
   SaatTipi        Varchar(15)   null

   constraint  pk_MtskSinavRandevuSaatTipi primary key (Id)
)

DELETE Lkp.MtskSinavRandevuSaatTipi
INSERT INTO Lkp.MtskSinavRandevuSaatTipi (Id, SaatTipi)
 VALUES 
  ( 0, N''),
  ( 1, N'08:20 - 08:55'),
  ( 2, N'09:00 - 09:35'),
  ( 3, N'09:40 - 10:15'),
  ( 4, N'10:20 - 10:55'),
  ( 5, N'11:00 - 11:35'),
  ( 6, N'11:40 - 12:15'),
  ( 7, N'13:15 - 13:50'),
  ( 8, N'13:55 - 14:30'),
  ( 9, N'14:35 - 15:10'),
  (10, N'15:15 - 15:50'),
  (11, N'15:55 - 16:30'),
  (12, N'16:35 - 17:10')

/*
  ( 2, N'08:20 - 08:55'),
  ( 3, N'09:00 - 09:35'),
  ( 4, N'09:40 - 10:15'),
  ( 5, N'10:20 - 10:55'),
  ( 6, N'11:00 - 11:35'),
  ( 7, N'11:40 - 12:15'),
  ( 8, N'13:15 - 13:50'),
  ( 9, N'13:55 - 14:30'),
  (10, N'14:35 - 15:10'),
  (11, N'15:15 - 15:50'),
  (12, N'15:55 - 16:30'),
  (13, N'16:35 - 17:10')
*/

-- ------------------------------------------------------- 
-- PersonelNoTipiId
-- ------------------------------------------------------- 
IF EXISTS (Select * From sys.objects WHERE name = 'MtskSinavRandevuPersonelNoTipi')
drop table [Lkp].[MtskSinavRandevuPersonelNoTipi]

create table Lkp.MtskSinavRandevuPersonelNoTipi
(
   Id               Int Unique Not Null,
   PersonelNoTipi   Varchar(20)   null

   constraint  pk_MtskSinavRandevuPersonelNoTipi primary key (Id)
)

INSERT INTO Lkp.MtskSinavRandevuPersonelNoTipi (Id, PersonelNoTipi)
 VALUES 
  ( 0, N''),
  ( 1, N'1. Usta öğretici'),
  ( 2, N'2. Usta öğretici'),
  ( 3, N'3. Usta öğretici')

commit transaction




/*CreateLkpTableEnd*/
